Feisty Lemonade Sloth

High

# `Eth0::mint` function will revert when oracle price drops, causing the minting of the Eth0 causing denial of service.

# Decription
The [`Eth0::mint`](https://github.com/sherlock-audit/2025-05-usual-eth0/blob/995d35f32c762aec7f678292b8702bc71aec1247/eth0-protocol/src/token/Eth0.sol#L129) function is called by the `DaoCollateral` contract when a user wants to swap `wstEth` for `Eth0`. The contract calls the oracle to get the current `wstEth/ETH` price, and based on that, the amount of `Eth0` is minted to the user.
The mint function checks if the totalSupply of Eth0 does not exceed the collateralBacking amount. If it does, the function reverts with the [AmountExceedBacking()](https://github.com/sherlock-audit/2025-05-usual-eth0/blob/995d35f32c762aec7f678292b8702bc71aec1247/eth0-protocol/src/token/Eth0.sol#L163) error.
The problem lies in the scenario where the price of `wstEth/ETH` decreases from a higher value to a lower value while still remaining within the deviation threshold. In such a case, the function will revert, preventing users from minting tokens.

```solidity
function mint(address to, uint256 amount) public {
        if (amount == 0) {
            revert AmountIsZero();
        }

        Eth0StorageV0 storage $ = _eth0StorageV0();
        $.registryAccess.onlyMatchingRole(ETH0_MINT);
        IOracle oracle = IOracle($.registryContract.getContract(CONTRACT_ORACLE));
        address treasury = $.registryContract.getContract(CONTRACT_TREASURY);

        // Check if minting would exceed the mint cap
        if (totalSupply() + amount > $.mintCap) {
            revert AmountExceedCap();
        }

        address[] memory collateralTokens = $.tokenMapping.getAllEth0CollateralTokens();

        uint256 wadCollateralBackingInETH = 0;
        for (uint256 i = 0; i < collateralTokens.length;) {
            address collateralToken = collateralTokens[i];
            uint256 collateralTokenPriceInETH = uint256(oracle.getPrice(collateralToken));
            uint8 decimals = IERC20Metadata(collateralToken).decimals();

            wadCollateralBackingInETH += Math.mulDiv(
                collateralTokenPriceInETH,
                IERC20(collateralToken).balanceOf(treasury),
                10 ** decimals
            );

            unchecked {
                ++i;
            }
        }
@>      if (totalSupply() + amount > wadCollateralBackingInETH) {
            revert AmountExceedBacking();
        }
        _mint(to, amount);
    }
```

# Impact
Denial of Service (DoS) due to a lower price than before.

# POC
1. A user1 calls the swap function to deposit `100e18` wstEth at a swapRate of `1.001e18`.
2. The amount of Eth0 that the user will mint is calculated as:
`1.001e18 * 100e18 / 1e18 = 100.1e18`.
3. Now, the totalSupply of Eth0 is `100.1e18`, and the collateralBacking is also `100.1e18`.
4. The swapRate reduces from `1.001e18 to 0.999e18`.
5. user2 calls the swap function to deposit `100e18` wstEth at a swapRate of `0.999e18`.
6. The amount of Eth0 that user2 will mint is calculated as:
`0.999e18 * 100e18 / 1e18 = 99.9e18`.
7. Now, the totalSupply of Eth0 becomes:
`100.1e18 + 99.9e18 = 200e18`.
8. However, the collateral token backing will be:
collateralToken balance of treasury (200e18) * current price rate (0.999e18) = `200e18 * 0.999e18` = `199.8e18`.
9. At this point, the totalSupply of the `Eth0` token exceeds the `collateralBackingInEth`. As a result, the function will revert, and user2 will not be able to mint.

# POC(coded)
Paste this in the `Eth0.t.sol` and run `forge test --mt test_revert_when_price_fluctuates -vvv`

```solidity
contract Oracle is IAggregator {
    uint256 s_answer = 1e18;

    function setAnswer(int256 _answer) external {
        s_answer = uint256(_answer);
    }

    function decimals() external view override returns (uint8) {
        return 18;
    }

    function description() external view override returns (string memory) {
        return "Mock Oracle";
    }

    function version() external view override returns (uint256) {
        return 1;
    }

    function getRoundData(uint80 _roundId)
        external
        view
        override
        returns (
            uint80 roundId,
            int256 answer,
            uint256 startedAt,
            uint256 updatedAt,
            uint80 answeredInRound
        )
    {
        return (_roundId, int256(s_answer), block.timestamp, block.timestamp, _roundId);
    }

    function latestRoundData()
        external
        view
        override
        returns (
            uint80 roundId,
            int256 answer,
            uint256 startedAt,
            uint256 updatedAt,
            uint80 answeredInRound
        )
    {
        return (1, int256(s_answer), block.timestamp, block.timestamp, 1);
    }
}

contract Eth0Test is SetupTest {
    Eth0 public eth0Token;
    LidoProxyWstETHPriceFeed lidoWstEthOracle;
    Oracle public oracle;

    event Blacklist(address account);
    event UnBlacklist(address account);
    event MintCapUpdated(uint256 newMintCap);

    function setUp() public virtual override {
        uint256 forkId =
            vm.createFork("mainnet");
        vm.selectFork(forkId);
        super.setUp();
        eth0Token = stbcToken;
        oracle = new Oracle();

        lidoWstEthOracle = new LidoProxyWstETHPriceFeed(WSTETH);

        vm.startPrank(admin);
        classicalOracle.initializeTokenOracle(WSTETH, address(oracle), 7 days, false);

        tokenMapping.addEth0CollateralToken(WSTETH);
        vm.stopPrank();

        // deal(WSTETH, treasury, type(uint128).max);
    }

    function test_revert_when_price_fluctuates() public {
        oracle.setAnswer(1.001e18);
        vm.startPrank(admin);
        registryAccess.grantRole(ETH0_MINT, admin);
        deal(WSTETH, treasury, 100e18);
        eth0Token.mint(alice, 100e18);
        vm.stopPrank();

        oracle.setAnswer(0.999e18);
        vm.startPrank(admin);
        deal(WSTETH, treasury, 100e18);
        eth0Token.mint(alice, 100e18);
        vm.stopPrank();
    }
}
```

The function will revert with `AmountExceedBacking()` error.

```sh
[336] 0x7f39C581F595B53c5cb19bD0b3f8dA6c935E2Ca0::decimals() [staticcall]
    │   │   └─ ← [Return] 18
    │   ├─ [534] 0x7f39C581F595B53c5cb19bD0b3f8dA6c935E2Ca0::balanceOf(treasury: [0x36eaB6CE7fedEDc098Ef98C41E83548A89147131]) [staticcall]
    │   │   └─ ← [Return] 100000000000000000000 [1e20]
    │   ├─ [0] console::log("totalSupply()", 100000000000000000000 [1e20]) [staticcall]
    │   │   └─ ← [Stop]
    │   ├─ [0] console::log("wadCollateralBackingInETH", 99900000000000000000 [9.99e19]) [staticcall]
    │   │   └─ ← [Stop]
    │   └─ ← [Revert] AmountExceedBacking()
    └─ ← [Revert] AmountExceedBacking()
```

# Recommendation
Modify the minting logic to account for price fluctuations within the deviation threshold, ensuring that the function does not unnecessarily revert.