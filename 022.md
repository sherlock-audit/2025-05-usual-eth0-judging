Main Mauve Mule

Medium

# Operation sequence in redemption process allows protocol undercollateralization

### Summary

The [DaoCollateral::_burnEth0TokenAndTransferCollateral()](https://github.com/sherlock-audit/2025-05-usual-eth0/blob/main/eth0-protocol/src/daoCollateral/DaoCollateral.sol#L472-L497) function first burns ETH0, then mints fees, and finally transfers collateral to users during redemption, creating a window where the protocol becomes undercollateralized and potentially blocking new user deposits.

### Root Cause

In [DaoCollateral::_burnEth0TokenAndTransferCollateral()](https://github.com/sherlock-audit/2025-05-usual-eth0/blob/main/eth0-protocol/src/daoCollateral/DaoCollateral.sol#L472-L497), the operation sequence creates a state where the protocol's collateralization ratio is temporarily violated. The function:

1. First burns all ETH0 tokens from the user (`$.eth0.burnFrom(msg.sender, eth0Amount)`)
2. Then mints a smaller amount as fees (`$.eth0.mint($.treasuryYield, stableFee)`)  
3. Finally transfers collateral to the user based on the non-fee portion (`IERC20Metadata(collateralToken).safeTransferFrom($.treasury, msg.sender, returnedCollateral)`)

This sequence creates a window where the total ETH0 supply is reduced by more than the value of the collateral removed, potentially leading to protocol undercollateralization:

```solidity
function _burnEth0TokenAndTransferCollateral(
    address collateralToken,
    uint256 eth0Amount,
    uint256 stableFee
) internal returns (uint256 returnedCollateral) {
    DaoCollateralStorageV0 storage $ = _daoCollateralStorageV0();
    // we burn the remaining ETH0 token
    uint256 burnedEth0 = eth0Amount - stableFee;
    // we burn all the ETH0 token
    $.eth0.burnFrom(msg.sender, eth0Amount);

    // If the CBR is on, the fees are forfeited from the yield treasury to favor the collateralization ratio
    if (stableFee > 0 && !$.isCBROn) {
@>      $.eth0.mint($.treasuryYield, stableFee);
    }

    // get the amount of collateral token for the amount of ETH0 burned by calling the oracle
    returnedCollateral = _getTokenAmountForAmountInETH(burnedEth0, collateralToken);
    if (returnedCollateral == 0) {
        revert AmountTooLow();
    }

    // we distribute the collateral token from the treasury to the user
@>  IERC20Metadata(collateralToken).safeTransferFrom($.treasury, msg.sender, returnedCollateral);
}
```

This is problematic because the [Eth0::mint()](https://github.com/sherlock-audit/2025-05-usual-eth0/blob/main/eth0-protocol/src/token/Eth0.sol#L129-L166) function enforces that collateral value should always equal or exceed the total ETH0 supply:

```solidity
// From Eth0.sol
if (totalSupply() + amount > wadCollateralBackingInETH) {
    revert AmountExceedBacking();
}
```
However, when minting the `stableFee` to the `treasuryYield`, the collateral value appears artificially high compared to the total ETH0 supply. This occurs because the user's ETH0 tokens are already burned  (decreasing total supply) but the corresponding collateral is not transferred out yet. Effectively, collateral that's already earmarked for the user is used  to satisfy the collateralization check, creating a false impression of adequate backing. 


### Internal Pre-conditions

1. The contract must not be in Counter Bank Run (CBR) mode, as fees are not minted in CBR mode
2. A user must call `redeem()` with a non-zero amount of ETH0 tokens
3. The price of the collateral asset must have dropped slightly since the user minted ETH0
4. The collateral ratio must be close to 1:1 before the redeem operation occurs

### External Pre-conditions

1. The price of the collateral asset relative to ETH must have decreased slightly since users minted ETH0
2. The oracle must report this lower price when the redeem operation occurs

### Attack Path

1. A user acquires some ETH0 tokens (through swap or transfer)
2. The price of the collateral asset decreases slightly
3. User calls `DaoCollateral::redeem()` with their ETH0 tokens and a specific collateral token
4. In `_burnEth0TokenAndTransferCollateral()`:
   - All user's ETH0 tokens are burned, reducing total supply, and collateral stays the same
   - A portion is minted back to treasury as fee (increasing total supply but by less than was burned), and collateral stays the same as before the burn
   - Collateral tokens are calculated and transferred to the user
5. After this transaction, the protocol becomes undercollateralized (total collateral value < total ETH0 supply)
6. New users cannot mint ETH0 tokens until collateral value increases or additional collateral is added


### Impact

The protocol's core invariant that collateral value should always equal or exceed ETH0 supply is violated. New users are temporarily prevented from minting ETH0 tokens until collateralization is restored by price increase.

### PoC

```solidity
// SPDX-License-Identifier: Apache-2.0

pragma solidity 0.8.20;

import {DaoCollateralTest} from "test/DaoCollateral.t.sol";
import {Eth0} from "src/token/Eth0.sol";
import {IERC20} from "openzeppelin-contracts/token/ERC20/IERC20.sol";
import {console2} from "forge-std/console2.sol";
import {AmountExceedBacking} from "src/errors.sol";

contract PoC is DaoCollateralTest {
    function setUp() public override {
        super.setUp();
        // Additional setup for PoC can be added here
    }

    function testSwapAndRedeemWhenPriceDrops() public {
        // Set the fee to 1%
        vm.startPrank(admin);
        daoCollateral.setRedeemFee(100); // 1% fee
        vm.stopPrank();

        //Arrange
        uint256 initialCollateral = 1000 ether;

        (address wstETH, Eth0 eth0) = setupCreationRwaLido();

        deal(wstETH, alice, initialCollateral);
        deal(wstETH, bob, initialCollateral);

        // Set the price of wstETH to 1.20
        _setOraclePrice(wstETH, 1.2 ether);

        // Swap
        vm.startPrank(alice);
        IERC20(wstETH).approve(address(daoCollateral), initialCollateral);
        daoCollateral.swap(wstETH, initialCollateral, 0);
        vm.stopPrank();

        vm.startPrank(bob);
        IERC20(wstETH).approve(address(daoCollateral), initialCollateral);
        daoCollateral.swap(wstETH, initialCollateral, 0);
        vm.stopPrank();

        // Assert that the swap was successful
        uint256 aliceEth0Balance = eth0.balanceOf(alice);
        console2.log("Alice's ETH0 balance after swap:", aliceEth0Balance);
        uint256 bobEth0Balance = eth0.balanceOf(bob);
        console2.log("Bob's ETH0 balance after swap:", bobEth0Balance);

        uint256 treasuryWstETHBalance = IERC20(wstETH).balanceOf(treasury);
        console2.log("Treasury's wstETH balance after swap:", treasuryWstETHBalance);

        console2.log("Total ETH0 supply:", eth0.totalSupply());

        // Lower the price of wstETH to 1.19
        _setOraclePrice(wstETH, 1.19 ether);

        // Redeem

        vm.startPrank(alice);
        daoCollateral.redeem(wstETH, aliceEth0Balance, 0);
        vm.stopPrank();

        aliceEth0Balance = eth0.balanceOf(alice);
        console2.log("Alice's ETH0 balance after redeem:", aliceEth0Balance);

        uint256 treasuryYieldEth0Balance = eth0.balanceOf(treasuryYield);
        console2.log("Treasury Yield's ETH0 balance after redeem:", treasuryYieldEth0Balance);

        treasuryWstETHBalance = IERC20(wstETH).balanceOf(treasury);
        console2.log("Treasury's wstETH balance after swap:", treasuryWstETHBalance);
        console2.log("Total ETH0 supply:", eth0.totalSupply());

        // Price bounces back to 1.20
        _setOraclePrice(wstETH, 1.2 ether);

        uint256 collateralValue = (treasuryWstETHBalance * 1.2 ether) / 1 ether;

        // Undercollateralized check
        assert(collateralValue < eth0.totalSupply());
        console2.log("Collateral value after price bounce:", collateralValue);

        vm.startPrank(alice);
        IERC20(wstETH).approve(address(daoCollateral), 50 ether);
        vm.expectRevert(AmountExceedBacking.selector);
        daoCollateral.swap(wstETH, 50 ether, 0);
        vm.stopPrank();
    }
    
}
```
Logs:
```logs
    Alice's ETH0 balance after swap: 1200000000000000000000
    Bob's ETH0 balance after swap: 1200000000000000000000
    Treasury's wstETH balance after swap: 2000000000000000000000
    Total ETH0 supply: 2400000000000000000000
    Alice's ETH0 balance after redeem: 0
    Treasury Yield's ETH0 balance after redeem: 12000000000000000000
    Treasury's wstETH balance after swap: 1001680672268907563026
    Total ETH0 supply: 1212000000000000000000
    Collateral value after price bounce: 1202016806722689075631
```

### Mitigation

Reorder operations so mint can actually check if the redeem will leave the protocol undercollateralized.
```diff
function _burnEth0TokenAndTransferCollateral(
    address collateralToken,
    uint256 eth0Amount,
    uint256 stableFee
) internal returns (uint256 returnedCollateral) {
    DaoCollateralStorageV0 storage $ = _daoCollateralStorageV0();
    // we burn the remaining ETH0 token
    uint256 burnedEth0 = eth0Amount - stableFee;
    
+   // Calculate the amount of collateral token first
+   returnedCollateral = _getTokenAmountForAmountInETH(burnedEth0, collateralToken);
+   if (returnedCollateral == 0) {
+       revert AmountTooLow();
+   }
    
    // we burn all the ETH0 token
    $.eth0.burnFrom(msg.sender, eth0Amount);

+   // Transfer collateral tokens from treasury to user before altering token supply
+   IERC20Metadata(collateralToken).safeTransferFrom($.treasury, msg.sender, returnedCollateral);

    // If the CBR is on, the fees are forfeited from the yield treasury to favor the collateralization ratio
    if (stableFee > 0 && !$.isCBROn) {
        $.eth0.mint($.treasuryYield, stableFee);
    }

-   // get the amount of collateral token for the amount of ETH0 burned by calling the oracle
-   returnedCollateral = _getTokenAmountForAmountInETH(burnedEth0, collateralToken);
-   if (returnedCollateral == 0) {
-       revert AmountTooLow();
-   }

-   // we distribute the collateral token from the treasury to the user
-   IERC20Metadata(collateralToken).safeTransferFrom($.treasury, msg.sender, returnedCollateral);
}
```