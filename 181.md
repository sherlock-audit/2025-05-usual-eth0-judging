Melodic Olive Swan

High

# Lack of slippage control in `redeemDao`

### Summary

-

### Root Cause

-

### Internal Pre-conditions

-

### External Pre-conditions

-

### Attack Path

Within the `redeem()` function, the user can specify the `minAmountOut` to control the slippage during the redemption from ETH0 to LST. The slippage control is required because it internally relies on the `_getTokenAmountForAmountInETH()` function to fetch the current on-chain price of LST and compute the amount of LST the user is entitled to. Thus, the on-chain price when the user submit the transaction might differ from the price when their transaction is actually executed on the blockchain.

https://github.com/sherlock-audit/2025-05-usual-eth0/blob/main/eth0-protocol/src/daoCollateral/DaoCollateral.sol#L538

```solidity
File: DaoCollateral.sol
538:     function redeem(address collateralToken, uint256 amount, uint256 minAmountOut)
539:         external
540:         nonReentrant
541:         whenRedeemNotPaused
542:         whenNotPaused
543:     {
544:         // Amount can't be 0
545:         if (amount == 0) {
546:             revert AmountIsZero();
547:         }
548: 
549:         // check that collateralToken is a collateral token
550:         if (!_daoCollateralStorageV0().tokenMapping.isEth0Collateral(collateralToken)) {
551:             revert InvalidToken();
552:         }
553:         uint256 stableFee = _calculateFee(amount, collateralToken);
554:         uint256 returnedCollateral =
555:             _burnEth0TokenAndTransferCollateral(collateralToken, amount, stableFee);
556:         // Check if the amount is greater than the minAmountOut
557:         if (returnedCollateral < minAmountOut) {
558:             revert AmountTooLow();
559:         }
560:         emit Redeem(msg.sender, collateralToken, amount, returnedCollateral, stableFee);
561:     }
```

However, the issue is that the `redeemDao()` does not have the same slippage control. Thus, when the DAO attempts to redeem assets, it might suffer a loss due to slippage.

https://github.com/sherlock-audit/2025-05-usual-eth0/blob/main/eth0-protocol/src/daoCollateral/DaoCollateral.sol#L564

```solidity
File: DaoCollateral.sol
564:     function redeemDao(address collateralToken, uint256 amount)
565:         external
566:         nonReentrant
567:         whenNotPaused
568:     {
569:         // Amount can't be 0
570:         if (amount == 0) {
571:             revert AmountIsZero();
572:         }
573: 
574:         _requireOnlyDaoRedeemer();
575:         // check that collateralToken is a collateral token
576:         if (!_daoCollateralStorageV0().tokenMapping.isEth0Collateral(collateralToken)) {
577:             revert InvalidToken();
578:         }
579:         uint256 returnedCollateral = _burnEth0TokenAndTransferCollateral(collateralToken, amount, 0);
580:         emit Redeem(msg.sender, collateralToken, amount, returnedCollateral, 0);
581:     }
```

### Impact

Loss of assets due to slippage


### PoC

_No response_

### Mitigation

Implement the slippage control for `redeemDao()` function.

```diff
- function redeemDao(address collateralToken, uint256 amount)
+ function redeemDao(address collateralToken, uint256 amount, uint256 minAmountOut)
    external
    nonReentrant
    whenNotPaused
{
    // Amount can't be 0
    if (amount == 0) {
        revert AmountIsZero();
    }

    _requireOnlyDaoRedeemer();
    // check that collateralToken is a collateral token
    if (!_daoCollateralStorageV0().tokenMapping.isEth0Collateral(collateralToken)) {
        revert InvalidToken();
    }
    uint256 returnedCollateral = _burnEth0TokenAndTransferCollateral(collateralToken, amount, 0);
    
+   // Check if the amount is greater than the minAmountOut
+   if (returnedCollateral < minAmountOut) {
+       revert AmountTooLow();
    
    emit Redeem(msg.sender, collateralToken, amount, returnedCollateral, 0);
}
```