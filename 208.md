Careful Peanut Aphid

Medium

# Minting of Eth0 can be infinitely DOS'd by the intended design to `soft-remove` collateral tokens

### Summary

The protocol includes the following, int he section "Please Discuss Any Design Choices You Made" :

"CollateralTokens are not removable by design, they can however be soft-removed by changing their pricefeed / upgrade."

But this soft-removal of collateral tokens, and decision to make collateral tokens not removable by design, is not handled properly in `ETH0` and will result in a COMPLETE DOS of the minting logic of new ETH0 - When a collateral token is soft removed, it will be impossible to mint any new Eth0 tokens.

### Root Cause

1. New collateral tokens are added in `ClassicalOracle` and their `priceFeed` / `DataScource` is populated for that specific token:

```solidity
    function initializeTokenOracle(
        address token,
        address dataSource,
        uint64 timeout,
        bool isStablecoin
    ) external {
        if (token == address(0)) revert NullAddress();
        if (dataSource == address(0)) revert NullAddress();
        // The timeout can't be zero and must be at most one week
        if (timeout == 0 || timeout > ONE_WEEK) revert InvalidTimeout();

        // slither-disable-next-line unused-return
        (, int256 answer,, uint256 updatedAt,) = IAggregator(dataSource).latestRoundData();
        if (answer <= 0 || updatedAt == 0 || block.timestamp > updatedAt + timeout) {
            revert OracleNotWorkingNotCurrent();
        }

        AbstractOracle.AbstractOracleStorageV0 storage $ = _abstractOracleStorageV0();
        $.registryAccess.onlyMatchingRole(DEFAULT_ADMIN_ROLE);

   ->     $.tokenToOracleInfo[token].dataSource = dataSource;
        $.tokenToOracleInfo[token].isStablecoin = isStablecoin;
        $.tokenToOracleInfo[token].timeout = timeout;
```


2. Because, collateral tokens by design, cannot be removed, the protocol attempts to implement a soft-removal by updating the price feed of the collateral token to a price feed that will cause the `_latestRoundData` function call to get the price of the collateral token - to fail. This is the method in which the protocol has decided to use to disable the use of the collateral token. So any time a user attempts to use that soft-removed collateral token, the process will fail when the protocol uses the collateral tokens price feed to fetch its price.

```solidity
    function _latestRoundData(address token) internal view override returns (uint256, uint256) {
        AbstractOracleStorageV0 storage $ = _abstractOracleStorageV0();
        IAggregator priceAggregatorProxy = IAggregator($.tokenToOracleInfo[token].dataSource);

        if (address(priceAggregatorProxy) == address(0)) revert OracleNotInitialized();

        uint256 decimals = priceAggregatorProxy.decimals();

        // slither-disable-next-line unused-return
        (, int256 answer,, uint256 updatedAt,) = priceAggregatorProxy.latestRoundData();
        if (answer <= 0) revert OracleNotWorkingNotCurrent();

        if (updatedAt > block.timestamp) revert OracleNotWorkingNotCurrent();

        if (block.timestamp > $.tokenToOracleInfo[token].timeout + updatedAt) {
            revert OracleNotWorkingNotCurrent();
        }
        return (uint256(answer), decimals);
    }
```

** The `_latestRoundData` is called by the protocol whenever the `getPrice` function is called by `eth0` 

** With this knowledge of how the protocol will soft-remove a collateral token, I will show below the way in which this will permenantly DOS the minting process of new ETH0.

### Internal Pre-conditions

1. No internal pre-conditions

### External Pre-conditions

1. Protocol decides to soft - remove a collateral token by updating its price feed / data source 

### Attack Path

The `mint` function of `Eth0` is called in 2 different scenarios (both critical) : When a user is depositing collateral to mint new eth0, and when a user is redeeming and the protocol mints the redeem fee in eth0.

** Lets jump to the `mint` function of `eth0` :

```solidity
  function mint(address to, uint256 amount) public {

...
 ->       address[] memory collateralTokens = $.tokenMapping.getAllEth0CollateralTokens();

        // @audit this is a point of failure, if one collateral token will always revert here, minting will infinite DOS
        uint256 wadCollateralBackingInETH = 0;
->        for (uint256 i = 0; i < collateralTokens.length;) {
               address collateralToken = collateralTokens[i];
->               uint256 collateralTokenPriceInETH = uint256(oracle.getPrice(collateralToken));
               uint8 decimals = IERC20Metadata(collateralToken).decimals();

            
               wadCollateralBackingInETH += Math.mulDiv(
                  collateralTokenPriceInETH,
                  IERC20(collateralToken).balanceOf(treasury),
                  10 ** decimals
            );

            unchecked {
                ++i;
            }
        }

        if (totalSupply() + amount > wadCollateralBackingInETH) {
            revert AmountExceedBacking();
        }
        _mint(to, amount);
    }
```

** In order of operations:
1. the function retrieves all of the collateral tokens , and because no collateral token can be removed, this array will include the soft-removed collateral token

2. The protocol begins a `for` loop, iterating over each of the collateral tokens (including the soft-removed token)

** This for loop is the single point of failure for the minting process, and if any of the collateral tokens fails within the for loop, the entire for loop will revert and always revert when that token reverts, completely DOS'ing the minting process.

3. In an attempt to get the price of the collateral token, the protocol calls `oracle.getPrice(collateralToken)` 

** Once again, if any of the collateral tokens will revert when fetching their price, this entire for loop will revert, and the entire minting process will revert and be DOS'd.

This is where the soft-removed token will fail and revert, breaking the logic of the minting process and DOS'ing it:

1. `getPrice` is called, which will then call `_latestRoundData` to get the price of the collateral token:

```solidity
    function getPrice(address token) public view override returns (uint256) {
->        (uint256 price, uint256 decimalsPrice) = _latestRoundData(token);
```

2. Remember, when the collateral token was soft-removed, its price feed was updated to fail at this point so that the collateral token is disabled from being used within the protocol:

```solidity
    function _latestRoundData(address token) internal view override returns (uint256, uint256) {
        AbstractOracleStorageV0 storage $ = _abstractOracleStorageV0();
        IAggregator priceAggregatorProxy = IAggregator($.tokenToOracleInfo[token].dataSource);
        if (address(priceAggregatorProxy) == address(0)) revert OracleNotInitialized();
        uint256 decimals = priceAggregatorProxy.decimals();
        // slither-disable-next-line unused-return
        (, int256 answer,, uint256 updatedAt,) = priceAggregatorProxy.latestRoundData();
        if (answer <= 0) revert OracleNotWorkingNotCurrent();
        if (block.timestamp > $.tokenToOracleInfo[token].timeout + updatedAt) {
            revert OracleNotWorkingNotCurrent();
        }
        return (uint256(answer), decimals);
```

** So, at this point the soft-removed collateral token will fail and revert because it was disabled from being used in the protocol, BUT it also results in the `for` loop in `mint` to revert - and since the collateral token cannot be removed, it will always be included in the `collateralTokens` array that is used and iterated over in the `mint` function. This will result in the inability to mint new `Eth0` and completely DOS the mint logic.


### Impact

The mint logic of `Eth0` is completely `DOS'd` and no new `Eth0` can be minted.

When a user `redeems`, the fee that the protocol takes will never be able to be minted, which will also result in the `redeem` flow failing under normal conditions.

When a user attempts to deposit collateral tokens (even accepted collateral tokens) - they will not be able to mint `eth0` and the entire deposit and mint flow will be reverted and fail, and user will not be able to deposit or receive any minted eth0.

### PoC

_No response_

### Mitigation

The protocol has made the design decision to not remove collateral tokens and only `soft-remove` them by updating their price feed which will make them fail whenever they are attempted to be used. But the protocol does not handle the failure of `soft-removed` tokens gracefully. The protocol needs to handle the failure of soft-removed tokens by skipping failing tokens in the `for` loop within the `mint` function.

Use a `try / catch` instead of relying on the single point of failure `for` loop which will revert always whenever a collateral token is soft removed:

```solidity
uint256 wadCollateralBackingInETH = 0;
for (uint256 i = 0; i < collateralTokens.length;) {
    address collateralToken = collateralTokens[i];
    try oracle.getPrice(collateralToken) returns (uint256 collateralTokenPriceInETH) {
        uint8 decimals = IERC20Metadata(collateralToken).decimals();
        uint256 balance = IERC20(collateralToken).balanceOf(treasury);

        wadCollateralBackingInETH += Math.mulDiv(
            collateralTokenPriceInETH,
            balance,
            10 ** decimals
        );
    } catch {
        // Skip this collateral token if it fails
    }

    unchecked {
        ++i;
    }
}
```

Instead of : 

https://github.com/sherlock-audit/2025-05-usual-eth0/blob/main/eth0-protocol/src/token/Eth0.sol#L144-L159