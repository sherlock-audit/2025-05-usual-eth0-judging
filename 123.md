Special Arctic Camel

High

# AbstractOracle depeg validation will detect only more than 20% drawdown for the price

### Summary

Protocol plans to work with different LST tokens, which are increasing in price over time. One of them is WstETH token, whose rate to Eth is [more than 1.2 currently](https://etherscan.io/address/0x7f39C581F595B53c5cb19bD0b3f8dA6c935E2Ca0#readContract#F10).

WstETH rate is computed [using `stEthPerToken()` function](https://github.com/sherlock-audit/2025-05-usual-eth0/blob/main/eth0-protocol/src/oracles/LidoWstEthOracle.sol#L86) inside `LidoProxyWstETHPriceFeed` contract. It is already more than 20% bigger than eth.

Protocol has implemented depeg validation for price of any collateral token, which is capped by `maxDepegThreshold` variable, that is [set to 1% by default](https://github.com/sherlock-audit/2025-05-usual-eth0/blob/main/eth0-protocol/src/oracles/AbstractOracle.sol#L78).
```solidity
function _checkDepegPrice(address token, uint256 wadPriceInETH) internal view {
        AbstractOracleStorageV0 storage $ = _abstractOracleStorageV0();

        // Skip the check if the token is not a stablecoin
        if (!$.tokenToOracleInfo[token].isStablecoin) return;

        uint256 threshold = Math.mulDiv($.maxDepegThreshold, SCALAR_ONE, BASIS_POINT_BASE);

        if (wadPriceInETH > SCALAR_ONE + threshold || wadPriceInETH < SCALAR_ONE - threshold) {
            revert StablecoinDepeg();
        }
    }
```
The check applies `maxDepegThreshold` to the 1e18 and then compares the price to be in the range.
As you can see with current default value of 1% the oracle will always detect depeg for wstETH. This is showed in the test "testPriceDepegsEvenWith1Percent".
As wstEth price is already more than 20% bigger than eth it means that in order to pass depeg validation of the upper side protocol has to set `maxDepegThreshold` at least to 21%. But this will also increase threshold for the down side, which can then detect the drawdown from 79% only, which is not how it was designed.

Also note, that `maxDepegThreshold` is same value for all collateral tokens that oracle provides price for.

### Root Cause

Protocol plans to work with different LST tokens, which are increasing in price over time. One of them is WstETH token, whose rate to Eth is [more than 1.2 currently](https://etherscan.io/address/0x7f39C581F595B53c5cb19bD0b3f8dA6c935E2Ca0#readContract#F10).

WstETH rate is computed [using `stEthPerToken()` function](https://github.com/sherlock-audit/2025-05-usual-eth0/blob/main/eth0-protocol/src/oracles/LidoWstEthOracle.sol#L86) inside `LidoProxyWstETHPriceFeed` contract. It is already more than 20% bigger than eth.

### Internal Pre-conditions

no

### External Pre-conditions

no

### Attack Path

no

### Impact

Oracle will always show depeg for WstEth with default `maxDepegThreshold` variable and will make protocol not protected from other tokens drawdown.

### PoC

// SPDX-License-Identifier: Apache-2.0

pragma solidity 0.8.20;

import {SetupTest} from "test/setup.t.sol";
import {ClassicalOracle} from "src/oracles/ClassicalOracle.sol";
import {ONE_WEEK, BASIS_POINT_BASE} from "src/constants.sol";
import {USDC, USDC_PRICE_FEED_MAINNET} from "src/mock/constants.sol";
import {IAggregator} from "src/interfaces/oracles/IAggregator.sol";
import {
    InvalidTimeout,
    SameValue,
    NullAddress,
    OracleNotWorkingNotCurrent,
    OracleNotInitialized,
    DepegThresholdTooHigh,
    StablecoinDepeg
} from "src/errors.sol";
import "forge-std/console.sol";

interface IWstETH {
    function stEthPerToken() external view returns (uint256);
}

contract ClassicalOracleTest is SetupTest, ClassicalOracle {
    address constant WSTETH_PRICE_FEED_MAINNET = 0xB9E1E3A9feFf48998E45Fa90847ed4D467E8BcfD;
    address constant WSTETH = 0x7f39C581F595B53c5cb19bD0b3f8dA6c935E2Ca0;

    function setUp() public override {
        uint256 forkId = vm.createFork("eth");
        vm.selectFork(forkId);
        super.setUp();
        classicalOracle = new ClassicalOracle();
        _resetInitializerImplementation(address(classicalOracle));
        classicalOracle.initialize(address(registryContract));

        // Initialize FRAX PriceFeed
        vm.prank(admin);
        classicalOracle.initializeTokenOracle(WSTETH, WSTETH_PRICE_FEED_MAINNET, 1 days, true);
    }


    function testPriceDepegsWith20Percent() public {
        //max threshold is 1% as default
        assertEq(classicalOracle.getMaxDepegThreshold(), 100);
        //make price feed return 18 decimals as wstETH
        vm.mockCall(
            WSTETH_PRICE_FEED_MAINNET,
            abi.encodeWithSelector(IAggregator.decimals.selector),
            abi.encode(18)
        );

        //mock wstETH PriceFeed to return 1e18 which works with threshold 1% and threshold 20%
        uint80 roundId = 2;
        int256 answer = 1e18;
        uint256 startedAt = 10;
        uint256 updatedAt = block.timestamp - 1;
        uint80 answeredInRound = 1;
        vm.mockCall(
            WSTETH_PRICE_FEED_MAINNET,
            abi.encodeWithSelector(IAggregator.latestRoundData.selector),
            abi.encode(roundId, answer, startedAt, updatedAt, answeredInRound)
        );

        vm.prank(admin);
        classicalOracle.initializeTokenOracle(WSTETH, WSTETH_PRICE_FEED_MAINNET, 1 days, true);

        //set 20% threshold
        vm.prank(admin);
        classicalOracle.setMaxDepegThreshold(2000);
        assertEq(classicalOracle.getMaxDepegThreshold(), 2000);
        assertEq(classicalOracle.getPrice(WSTETH), 1e18, "price should be 1");
        

        IWstETH wstETH = IWstETH(WSTETH);
        //get real wstEth rate to use in mock
        uint newAnswer = wstETH.stEthPerToken();
        console.log("wstEth rate: ", newAnswer);
        // Mock USDC PriceFeed with depegged value
        roundId = 3;
        startedAt = 10;
        updatedAt = block.timestamp - 1;
        answeredInRound = 2;
        
        vm.mockCall(
            WSTETH_PRICE_FEED_MAINNET,
            abi.encodeWithSelector(IAggregator.latestRoundData.selector),
            abi.encode(roundId, newAnswer, startedAt, updatedAt, answeredInRound)
        );
        
        vm.expectRevert(abi.encodeWithSelector(StablecoinDepeg.selector));
        classicalOracle.getPrice(WSTETH);
    }

    function testPriceWorksWith21Percent() public {
        //max threshold is 1% as default
        assertEq(classicalOracle.getMaxDepegThreshold(), 100);
        //make price feed return 18 decimals as wstETH
        vm.mockCall(
            WSTETH_PRICE_FEED_MAINNET,
            abi.encodeWithSelector(IAggregator.decimals.selector),
            abi.encode(18)
        );

        //mock wstETH PriceFeed to return 1e18 which works with threshold 1% and threshold 20%
        uint80 roundId = 2;
        int256 answer = 1e18;
        uint256 startedAt = 10;
        uint256 updatedAt = block.timestamp - 1;
        uint80 answeredInRound = 1;
        vm.mockCall(
            WSTETH_PRICE_FEED_MAINNET,
            abi.encodeWithSelector(IAggregator.latestRoundData.selector),
            abi.encode(roundId, answer, startedAt, updatedAt, answeredInRound)
        );

        vm.prank(admin);
        classicalOracle.initializeTokenOracle(WSTETH, WSTETH_PRICE_FEED_MAINNET, 1 days, true);

        //set 21% threshold
        vm.prank(admin);
        classicalOracle.setMaxDepegThreshold(2100);
        assertEq(classicalOracle.getMaxDepegThreshold(), 2100);
        assertEq(classicalOracle.getPrice(WSTETH), 1e18, "price should be 1");
        

        IWstETH wstETH = IWstETH(WSTETH);
        //get real wstEth rate to use in mock
        uint newAnswer = wstETH.stEthPerToken();
        console.log("wstEth rate: ", newAnswer);
        // Mock USDC PriceFeed with depegged value
        roundId = 3;
        startedAt = 10;
        updatedAt = block.timestamp - 1;
        answeredInRound = 2;
        
        vm.mockCall(
            WSTETH_PRICE_FEED_MAINNET,
            abi.encodeWithSelector(IAggregator.latestRoundData.selector),
            abi.encode(roundId, newAnswer, startedAt, updatedAt, answeredInRound)
        );
        
        classicalOracle.getPrice(WSTETH);
    }

    function testPriceDepegsEvenWith1Percent() public {
        //max threshold is 1% as default
        assertEq(classicalOracle.getMaxDepegThreshold(), 100);
        //make price feed return 18 decimals as wstETH
        vm.mockCall(
            WSTETH_PRICE_FEED_MAINNET,
            abi.encodeWithSelector(IAggregator.decimals.selector),
            abi.encode(18)
        );

        //mock wstETH PriceFeed to return 1e18 which works with threshold 1% and threshold 20%
        uint80 roundId = 2;
        int256 answer = 1e18;
        uint256 startedAt = 10;
        uint256 updatedAt = block.timestamp - 1;
        uint80 answeredInRound = 1;
        vm.mockCall(
            WSTETH_PRICE_FEED_MAINNET,
            abi.encodeWithSelector(IAggregator.latestRoundData.selector),
            abi.encode(roundId, answer, startedAt, updatedAt, answeredInRound)
        );

        vm.prank(admin);
        classicalOracle.initializeTokenOracle(WSTETH, WSTETH_PRICE_FEED_MAINNET, 1 days, true);
        assertEq(classicalOracle.getPrice(WSTETH), 1e18, "price should be 1");
        

        IWstETH wstETH = IWstETH(WSTETH);
        //get real wstEth rate to use in mock
        uint newAnswer = wstETH.stEthPerToken();
        console.log("wstEth rate: ", newAnswer);
        // Mock USDC PriceFeed with depegged value
        roundId = 3;
        startedAt = 10;
        updatedAt = block.timestamp - 1;
        answeredInRound = 2;
        
        vm.mockCall(
            WSTETH_PRICE_FEED_MAINNET,
            abi.encodeWithSelector(IAggregator.latestRoundData.selector),
            abi.encode(roundId, newAnswer, startedAt, updatedAt, answeredInRound)
        );
        
        vm.expectRevert(abi.encodeWithSelector(StablecoinDepeg.selector));
        classicalOracle.getPrice(WSTETH);
    }
}


### Mitigation

It's recommended to compare collateral price with itself and not with 1e18.