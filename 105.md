Elegant Grape Swift

High

# DoS on redeem() due to InsufficientBalance in case of a price drop

### Summary

DoS on redeem() due to InsufficientBalance in case of a price drop.

### Root Cause

In `redeem()`, [`_burnEth0TokenAndTransferCollateral`](https://github.com/sherlock-audit/2025-05-usual-eth0/blob/main/eth0-protocol/src/daoCollateral/DaoCollateral.sol#L489) will be called, which will transfer tokens based on wstETH price.

This means if two users swapped before a price drop, and then price dropped, only one of them will be able to redeem all his tokens, and the second redemption will fail due to InsufficientBalance.

### Internal Pre-conditions

N/A

### External Pre-conditions

wstETH price drop.

### Attack Path

Not exactly an attack, but the issue can happen as follows: 
1. Alice swaps 1000 wstETH for 1000 ETH0.
2. Bob swaps 1000 wstETH for 1000 ETH0.
3. Price drops to 0.9
4. Alice redeems 1000 ETH0 for 1100 wstETH.
5. Bob will try to redeem his 1000 ETH0, he's supposed to get 1100 wstETH, but the tx will fail due to InsufficientBalance.

### Impact

- DoS on a critical function, which is `redeem()`, as there won't be enough collateral to cover all the outstanding ETH0.

### PoC

Put the following test under `./test/PoC.t.sol`:
```solidity
// SPDX-License-Identifier: Apache-2.0
pragma solidity 0.8.20;

import "forge-std/console.sol";
import {ERC20} from "openzeppelin-contracts/token/ERC20/ERC20.sol";
import {Pausable} from "openzeppelin-contracts/utils/Pausable.sol";
import {IERC20} from "openzeppelin-contracts/token/ERC20/IERC20.sol";
import {IERC20Metadata} from "openzeppelin-contracts/token/ERC20/extensions/IERC20Metadata.sol";
import {IUSDC} from "test/interfaces/IUSDC.sol";
import {SetupTest} from "./setup.t.sol";
import {IRwaMock} from "src/interfaces/token/IRwaMock.sol";
import {RwaMock} from "src/mock/rwaMock.sol";
import {MyERC20} from "src/mock/myERC20.sol";
import {Eth0} from "src/token/Eth0.sol";
import {IERC20Errors} from "openzeppelin-contracts/interfaces/draft-IERC6093.sol";
import {Normalize} from "src/utils/normalize.sol";
import {Math} from "openzeppelin-contracts/utils/math/Math.sol";
import {IOracle} from "src/interfaces/oracles/IOracle.sol";
import {DaoCollateral} from "src/daoCollateral/DaoCollateral.sol";
import {IERC20Permit} from "openzeppelin-contracts/token/ERC20/extensions/IERC20Permit.sol";
import {SigUtils} from "test/utils/sigUtils.sol";
import {
    MAX_REDEEM_FEE,
    SCALAR_ONE,
    BASIS_POINT_BASE,
    ONE_YEAR,
    STETH,
    WSTETH,
    DAO_REDEMPTION_ROLE
} from "src/constants.sol";
import {
    SameValue,
    AmountTooLow,
    AmountTooBig,
    CBRIsTooHigh,
    CBRIsNull,
    RedeemMustNotBePaused,
    RedeemMustBePaused,
    SwapMustNotBePaused,
    SwapMustBePaused,
    RedeemFeeTooBig,
    NoOrdersIdsProvided,
    InvalidSigner,
    ExpiredSignature,
    InvalidDeadline,
    ApprovalFailed,
    RedeemFeeCannotBeZero
} from "src/errors.sol";
import "@openzeppelin/contracts/mocks/ERC1271WalletMock.sol";

contract PriceChangeInvariantPoC is SetupTest {
    ERC1271WalletMock public erc1271Mock;

    function setUp() public virtual override {
        uint256 forkId = vm.createFork("eth");
        vm.selectFork(forkId);
        super.setUp();

        erc1271Mock = new ERC1271WalletMock(alice);
        vm.deal(alice, 1 ether);

        address wstETH = address(WSTETH);
        vm.label(wstETH, "WSTETH Forked");
        if (!tokenMapping.isEth0Collateral(wstETH)) {
            vm.prank(admin);
            tokenMapping.addEth0CollateralToken(wstETH);
        }

        Eth0 eth0Token = stbcToken;
        whitelistPublisher(wstETH, address(eth0Token));
        _setupBucket(wstETH, address(eth0Token));
        _setOraclePrice(wstETH, 1e18); // 1 wstETH = 1 ETH
    }

    function test_RedeemInsufficientBalance() public {
        address wstETH = address(WSTETH);
        Eth0 eth0Token = stbcToken;
        uint256 amount = 1000e18;
        uint256 redeemFee = 100; // 1% (100 basis points)

        // Fund Alice, Bob, and treasury
        deal(wstETH, alice, amount);
        deal(wstETH, bob, amount);

        // Set redeemFee to 1%
        vm.prank(admin);
        daoCollateral.setRedeemFee(redeemFee);

        // Alice swaps 1000 wstETH for 1000 ETH0
        vm.startPrank(alice);
        IERC20(wstETH).approve(address(daoCollateral), amount);
        daoCollateral.swap(wstETH, amount, 0);
        vm.stopPrank();
        assertApproxEqAbs(eth0Token.balanceOf(alice), 1000e18, 1e15, "Incorrect ETH0 minted to Alice");

        // Bob swaps
        vm.startPrank(bob);
        IERC20(wstETH).approve(address(daoCollateral), amount);
        daoCollateral.swap(wstETH, amount, 0);
        vm.stopPrank();
        assertApproxEqAbs(eth0Token.balanceOf(bob), 1000e18, 1e15, "Incorrect ETH0 minted to Bob");

        // Price drops
        _setOraclePrice(wstETH, 0.9e18);
        assertEq(classicalOracle.getPrice(wstETH), 0.9e18, "Price not updated");

        uint256 totalEth0 = eth0Token.totalSupply();
        uint256 treasuryWstETH = IERC20(wstETH).balanceOf(treasury);
        uint256 treasuryCollateralEthValue = treasuryWstETH * classicalOracle.getPrice(wstETH) / 1e18;
        console.log("Alice wstETH balance before redeem: %d", IERC20(wstETH).balanceOf(alice));

        // Alice redeems
        vm.startPrank(alice);
        daoCollateral.redeem(wstETH, 1000e18, 0);
        vm.stopPrank();
        console.log("Alice wstETH balance  after redeem: %d", IERC20(wstETH).balanceOf(alice));

        // Bob attempts to redeem, but fails
        vm.startPrank(bob);
        vm.expectRevert();
        daoCollateral.redeem(wstETH, 1000e18, 0);
        vm.stopPrank();

    }
}
```

#### Output:
```shell
    │   ├─ [1287] WSTETH Forked::transferFrom(treasury: [0x36eaB6CE7fedEDc098Ef98C41E83548A89147131], bob: [0x02A02da2CB9795931fb68C8ae3d6237d2dD8e70e], 1100000000000000000000 [1.1e21])
    │   │   └─ ← [Revert] revert: ERC20: transfer amount exceeds balance
    │   └─ ← [Revert] revert: ERC20: transfer amount exceeds balance
    ├─ [0] VM::stopPrank()
    │   └─ ← [Return] 
    └─ ← [Stop] 
```

### Mitigation

_No response_