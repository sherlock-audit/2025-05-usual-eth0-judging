Elegant Grape Swift

High

# `redeem()` will burn ETH0 then call `mint()` before transferring from the treasury, breaking a core invariant and worsening the collateralization ratio

### Summary

`redeem()` will burn ETH0 then call `mint()` before transferring from the treasury, breaking a core invariant (minting must not be allowed if not backed by at least the same ETH value in the treasury) and worsening the collateralization ratio.

### Root Cause

In [`Eth0::mint()`](https://github.com/sherlock-audit/2025-05-usual-eth0/blob/main/eth0-protocol/src/token/Eth0.sol#L162), there is this condition which prevents the totalSupply from surpassing the collateral Backing:
```solidity
        if (totalSupply() + amount > wadCollateralBackingInETH) {
            revert AmountExceedBacking();
        }
```

When `redeem()` is called, `mint()` will be called [in the internal function [`_burnEth0TokenAndTransferCollateral`](https://github.com/sherlock-audit/2025-05-usual-eth0/blob/main/eth0-protocol/src/daoCollateral/DaoCollateral.sol#L485)] to mint the fee to treasuryYield:

The problem here is, the fee minting is happening after the user's ETH0 has been burned (totalSupply reduced), but before transferring funds from the treasury (ETH value in the treasury inflated):
```solidity
        uint256 burnedEth0 = eth0Amount - stableFee;
        // we burn all the ETH0 token
        $.eth0.burnFrom(msg.sender, eth0Amount);

        // If the CBR is on, the fees are forfeited from the yield treasury to favor the collateralization ratio
        if (stableFee > 0 && !$.isCBROn) {
            $.eth0.mint($.treasuryYield, stableFee);
        }

        // get the amount of collateral token for the amount of ETH0 burned by calling the oracle
        returnedCollateral = _getTokenAmountForAmountInETH(burnedEth0, collateralToken);
        if (returnedCollateral == 0) {
            revert AmountTooLow();
        }

        // we distribute the collateral token from the treasury to the user
        // slither-disable-next-line arbitrary-send-erc20
        IERC20Metadata(collateralToken).safeTransferFrom($.treasury, msg.sender, returnedCollateral);
```

It is mentioned in the contest's readme that the following is a core invariant that should be held at all times:
> ETH0 minting is not allowed if it is not backed by at least the same ETH amount of collateralTokens,

But this issue will break this invariant, since we're burning ETH0 before reducing the treasury's balance, leading to a inflated balance when `mint()` is called, allowing minting even if it's not backed by at least the same ETH value.

### Internal Pre-conditions

Undercollateralization.

### External Pre-conditions

N/A

### Attack Path

Not exactly an attack, but the issue can happen as follows:
1. Alice swaps 1000 wstETH for 1000 ETH0.
2. Bob swaps 1000 wstETH for 1000 ETH0.
3. Price drops to 0.98.
4. The system is undercollateralized, we have 2000 ETH0 and 1960 ETH value.
5. Alice calls `redeem()` to redeem 900 ETH0.
6. ETH0 will be burned before ETH value decreases, so now totalSupply becomes 1100, and ETH value is still 1960 ETH.
7. Minting 9 ETH0 will succeed (assuming %1 fee).
8. Collateral will be transferred to Alice, worth 900 ETH.
9. Now we have the `ETH0 totalSupply == 1109`, and `collateral value == 1069 ETH`.
10. The core invariant has been broken, and the collateralization ratio will be worse than before the redeem.

### Impact

Breaks a core protocol invariant mentioned explicitly in the readme, and worsens the system's undercollateralization.

### PoC

Here's a PoC demonstrating the issue, it demonstrates that even though the protocol was undercollateralized (aka, there is no backing to mint new ETH0), the minting succeeded:
```solidity
// SPDX-License-Identifier: Apache-2.0
pragma solidity 0.8.20;

import "forge-std/console.sol";
import {ERC20} from "openzeppelin-contracts/token/ERC20/ERC20.sol";
import {Pausable} from "openzeppelin-contracts/utils/Pausable.sol";
import {IERC20} from "openzeppelin-contracts/token/ERC20/IERC20.sol";
import {IERC20Metadata} from "openzeppelin-contracts/token/ERC20/extensions/IERC20Metadata.sol";
import {IUSDC} from "test/interfaces/IUSDC.sol";
import {SetupTest} from "./setup.t.sol";
import {IRwaMock} from "src/interfaces/token/IRwaMock.sol";
import {RwaMock} from "src/mock/rwaMock.sol";
import {MyERC20} from "src/mock/myERC20.sol";
import {Eth0} from "src/token/Eth0.sol";
import {IERC20Errors} from "openzeppelin-contracts/interfaces/draft-IERC6093.sol";
import {Normalize} from "src/utils/normalize.sol";
import {Math} from "openzeppelin-contracts/utils/math/Math.sol";
import {IOracle} from "src/interfaces/oracles/IOracle.sol";
import {DaoCollateral} from "src/daoCollateral/DaoCollateral.sol";
import {IERC20Permit} from "openzeppelin-contracts/token/ERC20/extensions/IERC20Permit.sol";
import {SigUtils} from "test/utils/sigUtils.sol";
import {
    MAX_REDEEM_FEE,
    SCALAR_ONE,
    BASIS_POINT_BASE,
    ONE_YEAR,
    STETH,
    WSTETH,
    DAO_REDEMPTION_ROLE
} from "src/constants.sol";
import {
    SameValue,
    AmountTooLow,
    AmountTooBig,
    CBRIsTooHigh,
    CBRIsNull,
    RedeemMustNotBePaused,
    RedeemMustBePaused,
    SwapMustNotBePaused,
    SwapMustBePaused,
    RedeemFeeTooBig,
    NoOrdersIdsProvided,
    InvalidSigner,
    ExpiredSignature,
    InvalidDeadline,
    ApprovalFailed,
    RedeemFeeCannotBeZero,
    AmountExceedBacking
} from "src/errors.sol";
import "@openzeppelin/contracts/mocks/ERC1271WalletMock.sol";

contract PriceChangeInvariantPoC is SetupTest {
    ERC1271WalletMock public erc1271Mock;

    function setUp() public virtual override {
        uint256 forkId = vm.createFork("eth");
        vm.selectFork(forkId);
        super.setUp();

        erc1271Mock = new ERC1271WalletMock(alice);
        vm.deal(alice, 1 ether);

        address wstETH = address(WSTETH);
        vm.label(wstETH, "WSTETH Forked");
        if (!tokenMapping.isEth0Collateral(wstETH)) {
            vm.prank(admin);
            tokenMapping.addEth0CollateralToken(wstETH);
        }

        Eth0 eth0Token = stbcToken;
        whitelistPublisher(wstETH, address(eth0Token));
        _setupBucket(wstETH, address(eth0Token));
        _setOraclePrice(wstETH, 1e18); // 1 wstETH = 1 ETH
    }

    function test_MintingBeforeTransferringBreaksInvariant() public {
        address wstETH = address(WSTETH);
        Eth0 eth0Token = stbcToken;
        uint256 amount = 1000e18;
        uint256 redeemFee = 100; // 1% (100 basis points)

        // Fund Alice, Bob
        deal(wstETH, alice, amount);
        deal(wstETH, bob, amount);

        // Set redeemFee to 1%
        vm.prank(admin);
        daoCollateral.setRedeemFee(redeemFee);

        // Alice swaps 1000 wstETH for 1000 ETH0
        vm.startPrank(alice);
        IERC20(wstETH).approve(address(daoCollateral), amount);
        daoCollateral.swap(wstETH, amount, 0);
        vm.stopPrank();
        assertApproxEqAbs(eth0Token.balanceOf(alice), 1000e18, 1e15, "Incorrect ETH0 minted to Alice");

        // Bob swaps
        vm.startPrank(bob);
        IERC20(wstETH).approve(address(daoCollateral), amount);
        daoCollateral.swap(wstETH, amount, 0);
        vm.stopPrank();
        assertApproxEqAbs(eth0Token.balanceOf(bob), 1000e18, 1e15, "Incorrect ETH0 minted to Bob");

        // Price drops
        _setOraclePrice(wstETH, 0.98e18);
        assertEq(classicalOracle.getPrice(wstETH), 0.98e18, "Price not updated");

        uint256 totalEth0 = eth0Token.totalSupply();
        uint256 treasuryWstETH = IERC20(wstETH).balanceOf(treasury);
        uint256 treasuryCollateralEthValue = treasuryWstETH * classicalOracle.getPrice(wstETH) / 1e18;
        console.log("Total ETH0 before redeem: %d, Collateral value before redeem: %d", totalEth0 / 1e18, treasuryCollateralEthValue / 1e18);
        console.log("Collateralization ratio before redeem: %d", treasuryCollateralEthValue * 1e18 / totalEth0);

        // Bob redeems 900
        vm.startPrank(bob);
        daoCollateral.redeem(wstETH, 900e18, 0);
        vm.stopPrank();

        totalEth0 = eth0Token.totalSupply();
        treasuryWstETH = IERC20(wstETH).balanceOf(treasury);
        treasuryCollateralEthValue = treasuryWstETH * classicalOracle.getPrice(wstETH) / 1e18;
        console.log("Total ETH0 after  redeem: %d, Collateral value after  redeem: %d", totalEth0 / 1e18, treasuryCollateralEthValue / 1e18);
        console.log("Notice that there was no backing for the amount minted, but minting succeeded, which worsened the collateralization ratio");
        console.log("Collateralization ratio after  redeem: %d", treasuryCollateralEthValue * 1e18 / totalEth0);
    }
}
```

#### Output:
```shell
[PASS] test_MintingBeforeTransferringBreaksInvariant() (gas: 1021766)
Logs:
  Total ETH0 before redeem: 2000, Collateral value before redeem: 1960
  Collateralization ratio before redeem: 980000000000000000
  Total ETH0 after  redeem: 1109, Collateral value after  redeem: 1069
  Notice that there was no backing for the amount minted, but minting succeeded, which worsened the collateralization ratio
  Collateralization ratio after  redeem: 963931469792605951
```

### Mitigation

Mint the fee after transferring from the treasury, and not before.