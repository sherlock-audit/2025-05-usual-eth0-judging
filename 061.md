Elegant Grape Swift

High

# `stEthPerToken` returns an stETH denominated price and not ETH, contradicting the intended use and causing incorrect usage of price

### Summary

`LidoProxyWstETHPriceFeed` returns the wstETH/stETH price via `stEthPerToken()` instead of the intended wstETH/ETH price. This mispricing affects the `DaoCollateral` and `Eth0` contracts.



### Root Cause

It is mentioned multiple times in the code comments that prices should be ETH denominated. Sources: [1](https://github.com/sherlock-audit/2025-05-usual-eth0/blob/main/eth0-protocol/src/oracles/LidoWstEthOracle.sol#L19), [2](https://github.com/sherlock-audit/2025-05-usual-eth0/blob/main/eth0-protocol/src/oracles/LidoWstEthOracle.sol#L26), [3](https://github.com/sherlock-audit/2025-05-usual-eth0/blob/main/eth0-protocol/src/oracles/LidoWstEthOracle.sol#L10), [4](https://github.com/sherlock-audit/2025-05-usual-eth0/blob/main/eth0-protocol/src/daoCollateral/DaoCollateral.sol#L379), [5](https://github.com/sherlock-audit/2025-05-usual-eth0/blob/main/eth0-protocol/src/daoCollateral/DaoCollateral.sol#L397) ...etc.

Also the `description()` function returns that it's a wstETH/ETH oracle. [see here](https://github.com/sherlock-audit/2025-05-usual-eth0/blob/main/eth0-protocol/src/oracles/LidoWstEthOracle.sol#L45)

The `WST_ETH_CONTRACT` is the address of the wrapped stETH contract (WSTETH). [see this comment](https://github.com/sherlock-audit/2025-05-usual-eth0/blob/main/eth0-protocol/src/oracles/LidoWstEthOracle.sol#L27).

The [`latestRoundData`](https://github.com/sherlock-audit/2025-05-usual-eth0/blob/main/eth0-protocol/src/oracles/LidoWstEthOracle.sol#L86) function calls `IWstETH(WST_ETH_CONTRACT).stEthPerToken()` to get the price of wstETH in ETH.

However, this is incorrect, since `stEthPerToken` returns the price in stETH and not ETH ! See the [documentation of Lido](https://docs.lido.fi/contracts/wsteth/#stethpertoken) for this function for proof.

stETH can depeg sometimes, so the `latestRoundData` function can report incorrect prices, leading to over/under-minting, over/under-valued redemptions in the `DaoCollateral` contract.



### Internal Pre-conditions

N/A

### External Pre-conditions

This can be a real issue if stETH depegs from ETH.

### Attack Path

Not necessarily an attack. The overminting can happen as follows:

- Let's assume stETH is currently worth 0.9 ETH.
- And stEthPerToken() returns 1.2 stETH.
- This means 1 wstETH = 1.2 stETH * 0.9 ETH/stETH = 1.08 ETH.
- The latestRoundData() should return the price ETH denominated, so it should return 1.08 ETH, but it's incorrectly returning 1.2 stETH (misinterpreted as 1.2 ETH).
- The oracle overprices wstETH (1.2 ETH vs. 1.08 ETH, ~11.11% higher). In `DaoCollateral.swap`, 1 wstETH gives 1.2 ETH0 instead of 1.08 ETH0, resulting in ~11.11% more ETH0 minted. For example, 1000 wstETH yields 1200 ETH0 instead of 1080 ETH0, overminting 120 ETH0 per 1000 wstETH.



### Impact

- Using incorrect prices, thinking they're ETH denominated but they're actually stETH denominated, which leads to over/under-minting in case of a `swap()`, or over/under-valued redemptions in case of a `redeem()` or `redeemDao()`.
- Contradicts with the documented price denomination (ETH documented VS stETH actually) which gives false promises to users and can erode their trust in the protocol.
- Incorrect use of price in the Eth0 contract (in the mint() function).

### PoC

Place the following test under `./test/PoC.t.sol`:
```solidity
// SPDX-License-Identifier: Apache-2.0
pragma solidity 0.8.20;

import "forge-std/console.sol";
import {ERC20} from "openzeppelin-contracts/token/ERC20/ERC20.sol";
import {IERC20} from "openzeppelin-contracts/token/ERC20/IERC20.sol";
import {IERC20Metadata} from "openzeppelin-contracts/token/ERC20/extensions/IERC20Metadata.sol";
import {SetupTest} from "./setup.t.sol";
import {Eth0} from "src/token/Eth0.sol";
import {DaoCollateral} from "src/daoCollateral/DaoCollateral.sol";
import {LidoProxyWstETHPriceFeed} from "src/oracles/LidoWstEthOracle.sol";
import {
    SCALAR_ONE,
    WSTETH
} from "src/constants.sol";
import {
    AmountTooLow,
    InvalidToken
} from "src/errors.sol";

contract OvermintExploitTest is SetupTest {
    /*//////////////////////////////////////////////////////////////
                            1. SETUP & HELPERS
    //////////////////////////////////////////////////////////////*/
    function setUp() public virtual override {
        uint256 forkId = vm.createFork("eth");
        vm.selectFork(forkId);
        super.setUp();

        // Set up wstETH as collateral
        address wstETH = address(WSTETH);
        vm.label(wstETH, "WSTETH Forked");
        if (!tokenMapping.isEth0Collateral(wstETH)) {
            vm.prank(admin);
            tokenMapping.addEth0CollateralToken(wstETH);
        }

        // Set oracle price for wstETH
        _setOraclePrice(wstETH, 1.2e18); // Incorrect: 1 wstETH = 1.2 ETH (should be 1.08 ETH)
        console.log("wstETH oracle price (incorrect):", classicalOracle.getPrice(wstETH), "(raw units)");

        // Fund attacker (bob) and treasury
        deal(wstETH, bob, 1000e18); // 1000 wstETH
        deal(wstETH, treasury, 1000e18); // Treasury wstETH
        deal(address(stbcToken), treasury, 1000e18); // Treasury ETH0
        console.log("Bob wstETH balance:", IERC20(wstETH).balanceOf(bob) / 1e18, "wstETH");
        console.log("Treasury wstETH balance:", IERC20(wstETH).balanceOf(treasury) / 1e18, "wstETH");
        console.log("Treasury ETH0 balance:", stbcToken.balanceOf(treasury) / 1e18, "ETH0");
    }

    function testOvermintVulnerability() public {
        // Assumptions: stETH = 0.9 ETH, stEthPerToken = 1.2 stETH, so 1 wstETH = 1.08 ETH
        // Oracle incorrectly returns 1.2, overpricing by ~11.11%

        // Step 1: Bob swaps 1000 wstETH for ETH0
        vm.startPrank(bob);
        IERC20(WSTETH).approve(address(daoCollateral), 1000e18);
        daoCollateral.swap(WSTETH, 1000e18, 1000e18); // Expect ~1200 ETH0
        uint256 eth0Balance = stbcToken.balanceOf(bob);
        console.log("After swap - Bob ETH0 balance:", eth0Balance / 1e18, "ETH0");
        console.log("After swap - Bob wstETH balance:", IERC20(WSTETH).balanceOf(bob) / 1e18, "wstETH");
        console.log("After swap - Treasury wstETH balance:", IERC20(WSTETH).balanceOf(treasury) / 1e18, "wstETH");

        // Verify overminting
        uint256 expectedEth0 = 1200e18; // 1000 wstETH * 1.2 ETH/wstETH
        uint256 correctEth0 = 1080e18; // 1000 wstETH * 1.08 ETH/wstETH
        assertApproxEqAbs(eth0Balance, expectedEth0, 1e15); // Got ~1200 ETH0
        assertEq(IERC20(WSTETH).balanceOf(bob), 0); // No wstETH left
        assertEq(IERC20(WSTETH).balanceOf(treasury), 2000e18); // Treasury gains 1000 wstETH

        // Impact: Bob got 1200 ETH0 (worth ~$1200 at 1 ETH0 = $1) for 1000 wstETH (worth ~$1080 at 1 wstETH = 1.08 ETH = $1080).
        console.log("Overminted ETH0:", (eth0Balance - correctEth0) / 1e18, "ETH0");
        vm.stopPrank();
    }
}
```

#### Output:
```bash
[PASS] testOvermintVulnerability() (gas: 192403)
Logs:
  wstETH oracle price (incorrect): 1200000000000000000 (raw units)
  Bob wstETH balance: 1000 wstETH
  Treasury wstETH balance: 1000 wstETH
  Treasury ETH0 balance: 1000 ETH0
  After swap - Bob ETH0 balance: 1200 ETH0
  After swap - Bob wstETH balance: 0 wstETH
  After swap - Treasury wstETH balance: 2000 wstETH
  Overminted ETH0: 120 ETH0
```

### Mitigation

After you get the price in stETH, use a [stETH/ETH price feed](https://data.chain.link/feeds/ethereum/mainnet/steth-eth) to convert prices from stETH to ETH. (the `LIDO_STETH_ORACLE_MAINNET` oracle is present in `constants.sol` but it's not used anywhere in the codebase).