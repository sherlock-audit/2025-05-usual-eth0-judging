Elegant Grape Swift

High

# Minting the fee will DoS small redemptions amounts but allow big ones in case of a price drop, which doesn't make sense

### Summary

Minting the fee instead of transferring it will DoS small `redeem()` amounts [due to `AmountExceedBacking`] and allow big ones in case of a price drop. Which doesn't make sense and worsens the system's collateralization.

### Root Cause

`redeem()` calls `_burnEth0TokenAndTransferCollateral()` which deduct the redeem fee, but then this fee is minted and not transferred, and [mint()](https://github.com/sherlock-audit/2025-05-usual-eth0/blob/main/eth0-protocol/src/token/Eth0.sol#L162) has this condition:
```solidity
        if (totalSupply() + amount > wadCollateralBackingInETH) {
            revert AmountExceedBacking();
        }
```

This condition will prevent small amounts redemptions, and allow big ones, which doesn't make sense (See Attack Path for examples).

### Internal Pre-conditions

N/A

### External Pre-conditions

A slight price drop.

### Attack Path

Not an attack, but the issue can happen as follows:
1. Alice swaps 1000 wstETH for 1000 ETH0.
2. Bob swaps 1000 wstETH for 1000 ETH0.
3. wstETH price drops to 0.98 for example.
4. Now the totalSupply of ETH0 is 2000, and ETH value in the treasury is 1960.
5. Redeem fee is 1%.
6. Alice tries to redeem 30e18 ETH. First, the 30e18 will be burned, so now we have the totalSupply == 1970. Then, the fee will be minted, which is 0.3e18: `1970e18 + 0.3e18 > 1960 == True`, so the redeem will revert with `AmountExceedBacking`.
7. But now Bob comes, he tries to redeem a bigger amount, 900e18.
8. First, the 900e18 will be burned, so now we have the totalSupply == 1100. Then, the fee will be minted, which is 9e18: `1100e18 + 9e18 > 1960 == False`. The redemption will happen with absolutely no problem.

### Impact

DoS'es small amount redemptions. [and allows big amount redemptions, which doesn't make sense and worsens the system's collateralization and can break the invariants mentioned in the readme]

### PoC

Put the following test under `./test/PoC.t.sol`:
```solidity
// SPDX-License-Identifier: Apache-2.0
pragma solidity 0.8.20;

import "forge-std/console.sol";
import {ERC20} from "openzeppelin-contracts/token/ERC20/ERC20.sol";
import {Pausable} from "openzeppelin-contracts/utils/Pausable.sol";
import {IERC20} from "openzeppelin-contracts/token/ERC20/IERC20.sol";
import {IERC20Metadata} from "openzeppelin-contracts/token/ERC20/extensions/IERC20Metadata.sol";
import {IUSDC} from "test/interfaces/IUSDC.sol";
import {SetupTest} from "./setup.t.sol";
import {IRwaMock} from "src/interfaces/token/IRwaMock.sol";
import {RwaMock} from "src/mock/rwaMock.sol";
import {MyERC20} from "src/mock/myERC20.sol";
import {Eth0} from "src/token/Eth0.sol";
import {IERC20Errors} from "openzeppelin-contracts/interfaces/draft-IERC6093.sol";
import {Normalize} from "src/utils/normalize.sol";
import {Math} from "openzeppelin-contracts/utils/math/Math.sol";
import {IOracle} from "src/interfaces/oracles/IOracle.sol";
import {DaoCollateral} from "src/daoCollateral/DaoCollateral.sol";
import {IERC20Permit} from "openzeppelin-contracts/token/ERC20/extensions/IERC20Permit.sol";
import {SigUtils} from "test/utils/sigUtils.sol";
import {
    MAX_REDEEM_FEE,
    SCALAR_ONE,
    BASIS_POINT_BASE,
    ONE_YEAR,
    STETH,
    WSTETH,
    DAO_REDEMPTION_ROLE
} from "src/constants.sol";
import {
    SameValue,
    AmountTooLow,
    AmountTooBig,
    CBRIsTooHigh,
    CBRIsNull,
    RedeemMustNotBePaused,
    RedeemMustBePaused,
    SwapMustNotBePaused,
    SwapMustBePaused,
    RedeemFeeTooBig,
    NoOrdersIdsProvided,
    InvalidSigner,
    ExpiredSignature,
    InvalidDeadline,
    ApprovalFailed,
    RedeemFeeCannotBeZero,
    AmountExceedBacking
} from "src/errors.sol";
import "@openzeppelin/contracts/mocks/ERC1271WalletMock.sol";

contract PriceChangeInvariantPoC is SetupTest {
    ERC1271WalletMock public erc1271Mock;

    function setUp() public virtual override {
        uint256 forkId = vm.createFork("eth");
        vm.selectFork(forkId);
        super.setUp();

        erc1271Mock = new ERC1271WalletMock(alice);
        vm.deal(alice, 1 ether);

        address wstETH = address(WSTETH);
        vm.label(wstETH, "WSTETH Forked");
        if (!tokenMapping.isEth0Collateral(wstETH)) {
            vm.prank(admin);
            tokenMapping.addEth0CollateralToken(wstETH);
        }

        Eth0 eth0Token = stbcToken;
        whitelistPublisher(wstETH, address(eth0Token));
        _setupBucket(wstETH, address(eth0Token));
        _setOraclePrice(wstETH, 1e18); // 1 wstETH = 1 ETH
    }

    function test_PreventSmallRedemptions() public {
        address wstETH = address(WSTETH);
        Eth0 eth0Token = stbcToken;
        uint256 amount = 1000e18;
        uint256 redeemFee = 100; // 1% (100 basis points)

        // Fund Alice, Bob
        deal(wstETH, alice, amount);
        deal(wstETH, bob, amount);

        // Set redeemFee to 1%
        vm.prank(admin);
        daoCollateral.setRedeemFee(redeemFee);

        // Alice swaps 1000 wstETH for 1000 ETH0
        vm.startPrank(alice);
        IERC20(wstETH).approve(address(daoCollateral), amount);
        daoCollateral.swap(wstETH, amount, 0);
        vm.stopPrank();
        assertApproxEqAbs(eth0Token.balanceOf(alice), 1000e18, 1e15, "Incorrect ETH0 minted to Alice");

        // Bob swaps
        vm.startPrank(bob);
        IERC20(wstETH).approve(address(daoCollateral), amount);
        daoCollateral.swap(wstETH, amount, 0);
        vm.stopPrank();
        assertApproxEqAbs(eth0Token.balanceOf(bob), 1000e18, 1e15, "Incorrect ETH0 minted to Bob");

        // Price drops
        _setOraclePrice(wstETH, 0.98e18);
        assertEq(classicalOracle.getPrice(wstETH), 0.98e18, "Price not updated");

        uint256 totalEth0 = eth0Token.totalSupply();
        uint256 treasuryWstETH = IERC20(wstETH).balanceOf(treasury);
        uint256 treasuryCollateralEthValue = treasuryWstETH * classicalOracle.getPrice(wstETH) / 1e18;
        console.log("Total ETH0: %d, Collateral value: %d", totalEth0 / 1e18, treasuryCollateralEthValue / 1e18);

        // Alice redeems
        vm.startPrank(alice);
        eth0Token.approve(address(daoCollateral), amount);
        vm.expectRevert(abi.encodeWithSelector(AmountExceedBacking.selector));
        daoCollateral.redeem(wstETH, 30e18, 0);
        vm.stopPrank();

        // Bob redeems 900
        vm.startPrank(bob);
        daoCollateral.redeem(wstETH, 900e18, 0);
        vm.stopPrank();

        totalEth0 = eth0Token.totalSupply();
        treasuryWstETH = IERC20(wstETH).balanceOf(treasury);
        treasuryCollateralEthValue = treasuryWstETH * classicalOracle.getPrice(wstETH) / 1e18;
        console.log("Total ETH0: %d, Collateral value: %d", totalEth0 / 1e18, treasuryCollateralEthValue / 1e18);
    }
}
```
### Output:
```shell
[PASS] test_PreventSmallRedemptions() (gas: 1078653)
Logs:
  Total ETH0: 2000, Collateral value: 1960
  Total ETH0: 1109, Collateral value: 1069
```

As you can see, first the collateralization ratio was 1 ETH0 for each 0.98 wstETH.
But after Bob redeemed, the ratio became 1 ETH0 for each 0.96 wstETH (1069 * 100 / 1109) (worsened the collateralization).



### Mitigation

Transfer the fee instead of minting it to prevent the DoS on small redemptions.