Low Raspberry Goat

Medium

# [M-3] `CBR` Logic not exempted in `DaoCollateral::redeemDao`resulting in reduced collateral returns for the protocol


### Reference Lines of Code

https://github.com/sherlock-audit/2025-05-usual-eth0/blob/995d35f32c762aec7f678292b8702bc71aec1247/eth0-protocol/src/daoCollateral/DaoCollateral.sol#L564


https://github.com/sherlock-audit/2025-05-usual-eth0/blob/995d35f32c762aec7f678292b8702bc71aec1247/eth0-protocol/src/daoCollateral/DaoCollateral.sol#L437-L439


### Summary

`DaoCollateral::redeemDao` is a permissioned function used by the protocol to exchange the `Eth0` they collected for the `collateralTokens`, this is evident as the function itself charges `0` stableFee but it doesn't account for the `CBR` and if the `CBR` is active then the `DaoCollateral::redeemDao` will redeem less collateral than expected

### Root Cause

The root cause of this issue is the `DaoCollateral::redeemDao` function doesn't account for the `CBR` and if the `CBR` is active then the `DaoCollateral::redeemDao` will redeem less collateral than expected

Snippet from `DaoCollateral::redeemDao`:

```solidity

function redeemDao(address collateralToken, uint256 amount)
        external
        nonReentrant
        whenNotPaused
    {
        // Amount can't be 0
        if (amount == 0) {
            revert AmountIsZero();
        }

        _requireOnlyDaoRedeemer();
        // check that collateralToken is a collateral token
        if (!_daoCollateralStorageV0().tokenMapping.isEth0Collateral(collateralToken)) {
            revert InvalidToken();
        }
@>        uint256 returnedCollateral = _burnEth0TokenAndTransferCollateral(collateralToken, amount, 0);
        emit Redeem(msg.sender, collateralToken, amount, returnedCollateral, 0);
    }
```

here `_burnEth0TokenAndTransferCollateral` is called , which has the following code: 

```solidity
 function _burnEth0TokenAndTransferCollateral(
        address collateralToken,
        uint256 eth0Amount,
        uint256 stableFee
    ) internal returns (uint256 returnedCollateral) {

          
        DaoCollateralStorageV0 storage $ = _daoCollateralStorageV0();        
        // we burn the remaining ETH0 token
        uint256 burnedEth0 = eth0Amount - stableFee;
        
        // we burn all the ETH0 token
        $.eth0.burnFrom(msg.sender, eth0Amount);

        // If the CBR is on, the fees are forfeited from the yield treasury to favor the collateralization ratio
        if (stableFee > 0 && !$.isCBROn) {
            $.eth0.mint($.treasuryYield, stableFee);
        }
        // get the amount of collateral token for the amount of ETH0 burned by calling the oracle
@>        returnedCollateral = _getTokenAmountForAmountInETH(burnedEth0, collateralToken);
        if (returnedCollateral == 0) {
            revert AmountTooLow();
        }
        // we distribute the collateral token from the treasury to the user
        // slither-disable-next-line arbitrary-send-erc20
        IERC20Metadata(collateralToken).safeTransferFrom($.treasury, msg.sender, returnedCollateral);

    }
```
this inturn calls the `_getTokenAmountForAmountInETH` function which has the following code:

```solidity
 function _getTokenAmountForAmountInETH(uint256 wadStableAmount, address collateralToken)
        internal
        view
        returns (uint256 amountInToken)
    {
        DaoCollateralStorageV0 storage $ = _daoCollateralStorageV0();
        amountInToken = _getQuoteInTokenFromETH(wadStableAmount, collateralToken);
        // if cbr is on we need to apply the coef to the collateral price
        // cbrCoef should be less than 1e18

@>        if ($.isCBROn) {
            amountInToken = Math.mulDiv(amountInToken, $.cbrCoef, SCALAR_ONE, Math.Rounding.Floor);
        }
    }
```
this `_getTokenAmountForAmountInETH` has an if statement which checks if `$.isCBROn` and alters the `amountInToken`(`returnedCollateral`) based on the `CBR coefficient`  but this if statement should be bypassed in `Dao::redeemDao` as it is a permissioned function which expects no fee deductions or any deductions to the `returned Collateral`.

### Internal Pre-conditions

`CBR` is active

### External Pre-conditions

Protocol calls `DaoCollateral::redeemDao` when `CBR` is active

### Attack Path

None

### Impact

`DaoCollateral::redeemDao` redeems less collateral than expected when `CBR` is active  as the `returnedCollateral` is received proportional to the `CBR coefficient`

### PoC


1. Add the following PoC to `DaoCollateral.t.sol` and run it using the following command in the terminal `forge test --mt test_RedeemDao -vv` and observe the logs
    
```solidity
 
     function test_RedeemDao() public {

        //Setup
        uint256 amount = 1e18;
 (RwaMock token, Eth0 stbc) = setupCreationRwa1(18);
      
        _whitelistRWA(address(token), daoredeemer);
 token.mint(daoredeemer, 1e18);

         
        //Setup to give redeemer some Eth0 through swap
        //(consider this as Fee Eth0 instead)
 vm.startPrank(daoredeemer);
 token.approve(address(daoCollateral), 1e18);
 daoCollateral.swap(address(token), 1e18, 0);
 vm.stopPrank();

    
       //admin activates CBR
 vm.prank(admin);
 daoCollateral.activateCBR(0.9 ether);
        
        // dao Redeemer redeems collateral by burning his Eth0 balance
 vm.startPrank(daoredeemer);
 daoCollateral.redeemDao(address(token), amount);
        
 vm.stopPrank();


        assertEq(stbc.balanceOf(daoredeemer), 0);
        // Dao Redeemer only redeems 0.9 ether instea of 1 ether
        assertEq(token.balanceOf(daoredeemer), 0.9 ether);
        

        //0.1 ether leftout in treasury 
        assertEq(IERC20(token).balanceOf(address(0x36eaB6CE7fedEDc098Ef98C41E83548A89147131)) ,0.1 ether);
        assertEq(IERC20(stbc).totalSupply() , 0);

         
 console.log("Treasury Collateral", IERC20(token).balanceOf(address(0x36eaB6CE7fedEDc098Ef98C41E83548A89147131)));
 console.log("Token TotalSupply()", IERC20(stbc).totalSupply());
 }


```


### Mitigation

To mitigate this issue :   
Add checks to account for `CBR` in `DaoCollateral::redeemDao`->`DaoCollateral::_getTokenAmountForAmountInETH` (internal Function) to check for ` _requireOnlyDaoRedeemer()` and if `msg.sender` is Dao Redeemer then do not account for `CBR`   